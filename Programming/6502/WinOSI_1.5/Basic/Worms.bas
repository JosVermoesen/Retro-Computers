
REM WORMS................

 0 REM        *************************
 1 REM               W O R M
 2 REM        *************************
 5 POKE56900,0
 10 GOTO1900
 100 FORBU=1TOBT
 102 FORZ=.TOML
 105 J=PEEK(KF)
 106 IFWTHENWL=F:F=0
 160 IFJ=DOTHENIFD+ITHEND=I:F=SD:GOTO200
 170 IFJ=RITHENIFD+1THEND=1:F=SR:GOTO200
 180 IFJ=UPTHENIFD-ITHEND=-I:F=SU:GOTO200
 190 POKECC,1:POKEKE,R1:IFLE=PEEK(KE)THENIFD+MTHEND=M:F=SL
 195 POKECC,.
 200 IFWTHENIFF=.THENFORQ=1TOZZ:NEXT:GOTO250
 206 IFF=.THENF=WL
 210 J=H:H=H+1:IFH>DSTHENH=.
 215 IFWFTHEN230
 220 POKES(T),B:S(T)=DP:T=T+1:IFT>DSTHENT=.
 230 N=S(J)+D:A=PEEK(N):POKEN,F:POKES(J),SB:S(H)=N
 240 IFA-BTHEN700
 250 NEXT
 255 IFB(BU)<.THEN500
 260 T(BU)=T(BU)-1:IFT(BU)THEN270
 261 K=B(BU):POKEK,B:POKEK+1,B:POKEK-1,B
 262 POKEK+I,B:POKEK-I,B:POKEK+IP,B:POKEK-IP,B
 263 POKEK+IM,B:POKEK-IM,B
 264 B(BU)=M:T(TU)=M
 270 TI=TI-LT:IFTI<.THEN1100
 280 PRINTS$SCTAB(TB)T$INT(TI);
 285 NEXT
 299 GOTO100
 500 FORQQ=.TO.
 510 C=UL+INT(RND(8)*X)+I*INT(RND(8)*Y)
 515 C=C+IP
 516 IFPEEK(C+1)-BTHEN590
 517 IFPEEK(C-1)-BTHEN590
 518 IFPEEK(C+I)-BTHEN590
 519 IFPEEK(C-I)-BTHEN590
 520 IFPEEK(C+IP)-BTHEN590
 521 IFPEEK(C-IP)-BTHEN590
 522 IFPEEK(C+IM)-BTHEN590
 523 IFPEEK(C-IM)-BTHEN590
 530 J=G(BU)
 531 POKEC+1,J:POKEC-1,J
 532 POKEC+I,J:POKEC-I,J
 533 POKEC+IP,J:POKEC-IP,J
 534 POKEC+IM,J:POKEC-IM,J
 545 POKEC,F9+RND(8)*9
 550 B(BU)=C
 555 T(BU)=INT(RND(8)*RT)+MT
 589 QQ=DP
 590 NEXT:GOTO270
 700 IFA=EDORA=SBTHEN1000
 705 S=S(H)
 710 FORQ=.TO7:J=PEEK(S+D(Q)):IFJ>F8THENIFJ<NITHENK=Q:Q=9
 720 NEXT
 730 C=S+D(K):J=J-F8
 735 T=T-J:IFT<.THENT=T+DS+1
 740 SC=SC+1:J=J-1:POKEC,J+F8
 750 PRINTS$SC;
 760 FORQQ=1TO100:NEXT
 770 IFJTHEN740
 780 FORQ=1TOBT:IFC=B(Q)THENK=Q:Q=9
 790 NEXT:FORQ=.TO8:IFC+D(Q)-STHENPOKEC+D(Q),B
 795 NEXT
 796 T(K)=M:BA=BA+1
 798 B(K)=M
 799 GOTO250
 999 STOP
 1000 FORQ=100TO10STEP-10
 1010 FORJ=1TOQ:NEXT
 1020 POKEN,F
 1030 FORJ=1TOQ:NEXT
 1040 POKEN,A
 1050 NEXT
 1060 Z=ML:NEXT
 1100 FORQ=1TO1000:NEXT
 1110 GOSUB5000
 1112 IFWFTHENIFNOTWTHENSC=INT(SC+60-TI)
 1115 PRINT
 1120 PRINT"    Score:"SC
 1130 PRINT
 1202 PRINT"  Highest score:"SH
 1210 IFSC>SHTHENSH=SC:PRINT:PRINT"   NEW HIGH SCORE!"
 1240 PRINT
 1245 POKE11,0:POKE12,253
 1246 IFSC>P(9)THENGOSUB4000
 1247 PRINT" ";
 1250 PRINT" Previous scores:"
 1255 PRINT" ";
 1260 PRINT" ----------------"
 1270 FORQ=0TO9
 1272 PRINTTAB(2);
 1274 PRINTMID$(I$,Q*3+1,3);
 1276 PRINTTAB(14);
 1278 J=P(Q):IFJ<100THENPRINT" ";
 1279 IFJ<10THENPRINT" ";
 1280 PRINTJ
 1282 NEXT
 1290 PRINT
 1300 PRINT"Same exact game?"
 1320 X=USR(X):J=PEEK(531):PRINT:IFJ=89THEN2900
 1330 RS=RND(8):GOTO2900
 1340 GOTO1320
 1900 GOSUB5000
 1905 I$="                              "
 1910 PRINT"  CONTROLS:"
 1920 PRINT:PRINT
 1930 PRINT"[ESC]       UP"
 1940 PRINT"[CTRL]      DOWN"
 1945 PRINT"[/]         LEFT"
 1950 PRINT"[R.S.]      RIGHT"
 1955 PRINT:PRINT
 1960 PRINT"      Try to get as"
 1962 PRINT" many boxes as you can"
 1964 PRINT" with your snake.  Each"
 1966 PRINT" time you hit a box,"
 1968 PRINT" you get the points in"
 1970 PRINT" the box.  Do not hit"
 1972 PRINT" the sides or yourself."
 1973 PRINT" If you do, the game"
 1974 PRINT" ends.  You have 60"
 1975 PRINT" seconds."
 1976 PRINT:PRINT
 1977 GOSUB4500:PRINT:PRINT
 1979 J=PEEK(57100)
 1980 PRINT" (hit SHIFT to start.)"
 1990 K=RND(8):IFJ=PEEK(57100)THEN1990
 1995 PRINT
 2000 UL=53510
 2001 TB=14
 2002 T9=7
 2003 TH=13
 2004 ML=4
 2005 LT=1/7*ML
 2006 F8=48:NI=58
 2007 BT=3
 2009 ZZ=50
 2010 CC=530
 2016 F9=49
 2019 RT=2:MT=1
 2020 KE=57088
 2025 KF=57100
 2030 R1=253:UP=222:DO=190:LE=247:RI=252
 2035 DO=255
 2040 I=32
 2050 IFPEEK(KE)>127THEN2080
 2060 REM...PUT IN C2/4 VALUES HERE
 2061 I=64:R1=2:UP=33:DO=65:RI=3:LE=8
 2065 DO=0
 2080 X=17:Y=17
 2090 DS=255:DIMS(DS)
 2096 DIMB(BT),T(BT),G(BT)
 2100 SB=226
 2110 B=32:M=-1
 2120 IM=I-1:IP=I+1
 2130 DP=64000
 2150 ED=187
 2160 SD=191:SL=192:SU=193:SR=194
 2200 DIMD(8):D(0)=I:D(1)=-I:D(2)=1:D(3)=-1:D(4)=IP:D(5)=-IP
 2210 D(6)=IM:D(7)=-IM
 2300 G(1)=35
 2310 G(2)=42
 2320 G(3)=3
 2330 S$=CHR$(13)+" SCORE"
 2340 T$="TIME"
 2350 RS=RND(8)
 2400 TE=14
 2900 FORQ=0TODS:S(Q)=DP:NEXT
 2905 FORQ=1TOBT:B(Q)=M:T(Q)=M:NEXT:BA=BT
 2910 SC=0
 2920 TI=60
 2930 H=9:T=0
 2940 S(H)=UL+4+4*I
 2950 D=1
 2960 F=SR
 2970 J=RND(-RS)
 3000 GOSUB5000
 3001 J=4
 3002 J1=J+1
 3010 FORQ=UL-1-ITOUL-I+X+J
 3020 POKEQ,ED:POKEQ+I*(Y+J1),ED:NEXT
 3030 FORQ=UL-1TOUL-1+I*(Y+J)STEPI
 3040 POKEQ,ED:POKEQ+X+J1,ED:NEXT
 3045 POKES(H),232
 3099 GOTO100
 3400 Q=P(J):PRINTTAB(TE);:IFQ<100THENPRINT" ";:IFQ<10THENPRINT" ";
 3410 PRINTQ:J=J+1:RETURN
 3500 N$="":J=-1:FORQ=.TO9
 3502 IFSC>P(Q)THENIFJ=-1THENJ=Q:N$=N$+A$:GOTO3506
 3504 IFJ=-1THENN$=N$+MID$(I$,Q*3+1,3):GOTO3506
 3505 N$=N$+MID$(I$,(Q-1)*3+1,3)
 3506 NEXT:IFJ<.THEN3550
 3507 IFJ=9THENP(9)=SC:GOTO3530
 3510 FORQ=9TOJ+1STEP-1:P(Q)=P(Q-1):NEXT:P(J)=SC
 3530 I$=N$
 3550 RETURN
 4000 PRINT" Your initials? ";
 4010 A$="":FORQ=1TO3:X=USR(X):J=PEEK(531):IFJ=13THENQ=9:GOTO4030
 4020 PRINTCHR$(J);:A$=A$+CHR$(J)
 4030 NEXT:PRINT
 4032 IFJ=1THENA$=A$+"  "
 4035 J=LEN(A$):IFJ=.THENA$="   "
 4045 IFJ=2THENA$=A$+" "
 4050 GOTO3500
 4500 POKE11,0:POKE12,253
 4502 PRINT" Standard format? ";
 4503 GOSUB4600:PRINT:PRINT
 4504 IFX=1THENW=0:WF=0:RETURN
 4510 PRINT" Constant motion? ";
 4520 GOSUB4600
 4521 PRINT:PRINT
 4525 W=X-1
 4530 PRINT" Always growing?  ";:GOSUB4600
 4535 WF=X
 4550 RETURN
 4600 X=USR(X):X=0:IFPEEK(531)=78THENPRINT"NO";:RETURN
 4610 X=1:PRINT"YES";:RETURN
 5000 A1=PEEK(129):A2=PEEK(130):POKE129,0:POKE130,212:H$=" "
 5007 FORZP=1TO15:PRINT:NEXT:POKE55104,32
 5010 FORS=1TO7:H$=H$+H$+" ":NEXT:POKE129,A1:POKE130,A2:RETURN
