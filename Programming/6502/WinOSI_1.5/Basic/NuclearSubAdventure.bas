REM Nuclear sub adventure

 6 DIMD$(11),O$(41),P(3):GOSUB970:FORI=1TO11:READD$(I):NEXT
 7 FORI=0TO41:READO$(I):NEXT:W=23:IFPEEK(57088)<127THENW=63
 8 GOSUB175:S=5:L=1:GOSUB715:FORI=0TO32:PRINT:NEXT:GOTO225
 9 IFFTHENT=T+.5:IFT=100THENPRINT:GOSUB750:GOTO925
 10 IFFTHENIFT/10=INT(T/10)THENPRINT:GOSUB750:GOSUB740
 15 IFUANDPEEK(A+6)THEN945
 20 IFPEEK(A+12)=0ANDPEEK(A+4)THEN900
 25 IFPEEK(A+13)=0ANDPEEK(A+4)THEN910
 30 Z=Z+1:IF(Z>0ANDPEEK(A+6))ANDL=6THENGOSUB990:GOTO945
 35 PRINT:POKE129,172:POKE130,31:GOSUB175
 40 INPUT"Your Command";Z$:IFLEN(Z$)=0THEN40
 45 A$=LEFT$(Z$,2):FORI=1TO23:PRINT"-";:NEXT:PRINT
 50 PRINT:FORI=LEN(Z$)TO1STEP-1:IFMID$(Z$,I,1)<>" "THENNEXT:GOTO75
 55 B$=MID$(Z$,I+1,2)
 60 FORI=1TOLEN(W$)STEP2:IFA$=MID$(W$,I,2)THENJ=(I+1)/2:GOTO70
 65 NEXT:GOTO725
 70 GOSUB100:IFI<=LEN(O$)THEN90
 75 IFA$="IN"THENJ=0:GOTO375
 80 IFA$="LO"THENS=5:GOTO265
 85 GOTO725
 90 ONJGOTO200,230,340,395,405,490,490,540,540,605,615,640,665,230
 100 FORI=1TOLEN(O$)STEP2:IFB$=MID$(O$,I,2)THENS=(I+1)/2-1:RETURN
 104 NEXT:RETURN
 105 DATATHE CONTROL ROOM,THE CONNING TOWER,THE ENGINE ROOM
 110 DATATHE BATTERY ROOM,THE GALLEY,A FLOODED COMPARTMENT,A TOOL ROOM
 115 DATA THE MISSLE ROOM,THE TORPEDO ROOM,THE CREW'S QUARTERS
 120 DATA THE REACTOR ROOM,FORWARD,AFT,UP,DOWN,HUNGRY MORAY EEL
 125 DATA POTS,PLATES,RUBBER GLOVES,MATCHES,SCUBA GEAR
 126 DATA BOX OF WASHERS
 130 DATA UNDERWATER LANTERN,WRENCH,SLEDGE HAMMER
 135 DATA OXYACETYLENE TORCH,STORAGE BATTERIES,ELECTRIC EEL,FISH
 140 DATACOMPUTER BOARD,HEAVY METAL CHAIR,REACTOR TEMP.DIAL
 145 DATADEPTH GAUGE,COMPUTER,PERISCOPE,PARTS CABINET
 150 DATAWARNING NOTICE,SINK,LOCKER,MISSILE TUBES
 155 DATATORPEDO TUBES
 160 DATAESCAPE HATCH,TURBINES,ENGINES,HATCH IN DECK,BUNKS
 165 DATAREACTOR,SMASHED COOLANT PUMPS,PUDDLE ON FLOOR,CHEST
 170 DATAHATCH IN BULKHEAD,HATCH TO MISSLE ROOM ,JAGGED HOLE
 175 W$="GOLOTADROPPUPOHIBRHOLISHFIRE
 180 O$="FOAFUPDOEEPOPLGLMASCWALAWRSLOXBAELFIBOCHDIGACOCAPENOSILOMITO
 185 O$=O$+"ESTUENHABURECOPUCHHAHAHOPIBRACARHI":RETURN
 200 IFS=41ANDL=6THENPRINT"You're too BIG to fit!":GOTO9
 205 IFL=9ANDE=1AND(S=29ORS=31)THEN995
 210 IFS>3THEN895
 215 IFL=10ANDPEEK(A+1)=LANDS<>1THEN960
 220 IFP(S)>0THENL=P(S):GOSUB715:S=5
 225 PRINT:PRINT"You are in--":PRINT:PRINTD$(L)
 230 IFS=15THENPRINT"ACID!":GOTO9
 235 IFS=31ANDL=8THENS=28
 240 IFS=31ANDL=9THENS=29
 245 IFS=19ANDL=6THEN855
 250 IFS<20ORS>29THEN265
 255 IFPEEK(A+S-3)<>LTHEN880
 260 ONS-19GOTO730,755,765,775,790,800,805,825,850,870
 265 IFNOTUOR(QAND(PEEK(A+8)=LORPEEK(A+8)=0))THEN275
 270 PRINT:PRINT"There's no LIGHT!":PRINT"You can't see!":GOTO9
 275 PRINT:PRINT"You can SEE--":PRINT
 280 IFL=11ANDS=2THEN915
 285 IFS<4THENIFP(S)>0THENPRINTD$(P(S)):GOTO9
 290 J=0:FORI=A+1TOA+38:IFPEEK(I)=LTHENPRINTO$(I-A+3):J=1
 295 NEXT:IFJ=0THENPRINT"NOTHING
 300 IFL=10ANDHANDNOTUTHENPRINT:PRINT:PRINT"Sound of water sloshing!
 305 PRINT:PRINT"Obvious Exits are--":PRINT:FORI=0TO3
 310 IFL=10ANDHTHENP(3)=6
 312 IFL=1ANDRTHENP(1)=8
 315 IFL=5ANDMHTHENP(0)=10
 320 IFP(I)>0THENPRINTO$(I)
 325 NEXT:PRINT
 330 IFL=6ANDPEEK(A+6)THENPRINT"Water is COLD! --GASP-- ":PRINT"HURRY!
 335 GOTO9
 340 IFS=42THENPRINT"NOT a good idea!":GOTO9
 345 IFS<4ORS>19THEN895
 350 IFPEEK(A+S-3)<>LANDPEEK(A+S-3)<>L+50THEN880
 355 IFC>3THENPRINT"You're carrying TOO MUCH":GOTO9
 360 POKEA+S-3,0:C=C+1
 365 IFS=4THEN960
 370 GOTO905
 375 PRINT"You're carrying:":FORI=1TO16
 380 IFPEEK(A+I)=0THENPRINTO$(I+3):J=1
 385 NEXT:IFJ=0THENPRINT"NOTHING
 390 GOTO9
 395 IFPEEK(A+S-3)THEN940
 400 POKEA+S-3,L:C=C-1:GOTO905
 405 IF(S=31ORS=33)ANDL=8THENS=28
 410 IFS=31ANDL=9THENS=29
 415 IFS=19ANDL=6THENS=38
 420 IFS=35ANDL=11THEN935
 425 IFS=23ANDL=2THENO$(24)="Open CABINET":POKEA+15,52:GOTO265
 430 IFS=27ANDL=6THENO$(27)="Open LOCKER":LO=-1:GOTO265
 435 IFS=38ANDL=6THENO$(38)="Open CHEST":D=-1:GOTO265
 440 IFS<>33THEN470
 445 IFL=2THENPRINT"No Way! Its SMASHED!":GOTO9
 450 IFNOTFTHEN885
 452 IFL=1THENPRINT"It JAMMED!":GOTO9
 455 IFL=5ANDFTHENMH=-1:GOTO965
 460 IFL<>10THEN895
 465 IFNOTHTHENPRINT"ScreeecH!":PRINT"Hinges are CORRODED!":GOTO9
 470 IFS=28OR(S=29ANDNOTE)THENPRINT"Tube loaded! Can't open!":GOTO9
 475 IFS<>29THEN895
 480 IFNOTUTHENPRINT"High water PRESSURE":PRINT"Can't open!":GOTO9
 485 E=1:O$(29)="Open TORPEDO Tube":GOTO265
 490 IF(S<19ANDPEEK(A+S-3))OR(S=44ANDPEEK(A+12))THEN940
 495 J=S:S=0:INPUT"Where";B$:B$=LEFT$(B$,2):GOSUB100
 500 IFJ=44AND(S=33ORS=46)THENH=-1:O$(33)="Open HATCH":GOTO9
 505 IFJ=44THEN955
 510 IFJ<>18THEN920
 515 IFS<>22ORUTHEN920
 520 SAVE:F=-1:POKEA+15,50:PRINT"Computer ON-LINE":PRINT:GOSUB990
 525 PRINT"DANGER! REACTOR COOLANT"
 530 PRINT"PUMPS DESTROYED":PRINT:PRINT"CORE TEMPERATURE RISING!
 535 POKE517,0:C=C-1:GOTO9
 540 J=S:INPUT"With what";B$:B$=LEFT$(B$,2):GOSUB100
 545 IFPEEK(A+S-3)THEN940
 550 IFS=19THENPRINT:PRINT"Clang!!!!":PRINT:IFJ=33ANDL=1THEN602
 560 IFJ<>42ORS<>13THEN920
 563 IFL<>11THENPRINT:PRINT" Not Here!":GOTO 9
 565 FORI=0TO25:PRINT:NEXT:FORI=1TOW:PRINTCHR$(193);:NEXT:POKE518,30
 570 PRINT:FORI=0TO15:PRINTTAB(RND(1)*W)"o":NEXT
 575 PRINT:PRINT"Ship is flooding!":PRINT
 580 PRINT"Core temp dropping!":GOSUB990:PRINT"Safety level attained
 585 GOSUB990 :PRINT"ELECTRICAL FAILURE!":PRINT"Systems shorting out!
 590 GOSUB990:PRINT"Computer MALFUNCTION!":PRINT"Complete power loss!
 595 POKE518,0:GOSUB990:F=0:U=-1:POKEA+14,8:POKEA+1,10:POKEA+13,4
 600 POKEA+34,50:POKEA+38,6:GOTO9
 602 O$(40)="Open HATCH":R=-1:GOTO225
 605 IFS=43ANDL<>6THENZ=-3:GOTO905
 610 GOTO895
 615 IFS=11ANDPEEK(A+8)=0THENQ=-1:GOTO225
 620 IF(S=8ANDPEEK(A+5))OR(S=11ANDPEEK(A+8))THEN940
 625 IFS<>8THEN895
 630 FORI=0TO6:PRINTTAB(RND(1)*18)"SPUTTER":PRINT:NEXT
 635 PRINT:GOSUB990 :PRINT"They're WET!":GOTO9
 640 IFS=28ORS=29THEN665
 645 IFPEEK(A+13)ORPEEK(A+1)<>10ORL<>10THEN920
 650 POKEA+1,50
 655 PRINT"The Moray Eel has fled":PRINT"down into":PRINTD$(6)
 660 PRINT"and out a jagged HOLE":PRINT"in the side!":GOTO9
 665 IFNOTFTHEN885
 670 IFS<>28ANDS<>29THEN920
 675 IFS=28THEN690
 680 SAVE:PRINT"FF";:FORI=1TO14:PRINT"f";:NEXT:PRINT"iiittt":POKE517,0
 685 E=-1:GOTO9
 690 POKE518,5:A$="WHOOOOOSSHH":FORI=0TO22:PRINT:NEXT:FORI=1TO11
 695 PRINTTAB(35)MID$(A$,I,1):NEXT:FORI=0TO25:PRINT:NEXT:POKE518,170
 700 GOSUB990 :PRINT"You just started WORLD":PRINT"WAR THREE!!":PRINT
 705 PRINT"There's nothing for you":PRINT"to go back to now!"
 710 GOSUB990:POKE518,0:GOTO930
 715 J=0:FORI=B+(L-1)*4TOB+(L-1)*4+3:P(J)=PEEK(I)-65:J=J+1:NEXT
 720 RETURN
 725 PRINT:PRINT"I DON'T UNDERSTAND":PRINTZ$:PRINT:GOTO40
 730 IFNOTFTHEN885
 735 GOSUB740:PRINT"Danger of meltdown is":PRINT"increasing!":GOTO9
 740 PRINT"Core temperature:"T"%":PRINT"of secondary criticality
 745 PRINT:RETURN
 750 PRINT"ALERT    ALERT    ALERT":PRINT:RETURN
 755 IFFTHENPRINT"Depth Reading:SEA BOTTOM":GOTO9
 760 GOTO885
 765 IFFTHEN875
 770 PRINT"Access Panel OPEN-One":PRINT"Circuit Board MISSING":GOTO9
 775 J=0:IFPEEK(A+15)=52THENPRINTO$(18):J=1
 780 IFJ=0THEN875
 785 GOTO9
 790 PRINT"There's FISH, a hungry":PRINT"looking MORAY EEL and
 795 PRINT"a LOT of WATER out there.":GOTO9
 800 PRINT"DANGER-Highly corrosive":PRINT"BATTERY ACID!":GOTO9
 805 J=0:IFPEEK(A+4)>49THENPOKEA+4,55:PRINTO$(7):J=1
 810 IFPEEK(A+5)>49THENPOKEA+5,55:PRINTO$(8):J=1
 815 IFJ=0THEN875
 820 GOTO9
 825 J=0:IFLO=0THEN875
 830 IFPEEK(A+7)>49THENPOKEA+7,56:PRINTO$(10):J=1
 835 IFPEEK(A+8)>49THENPOKEA+8,56:PRINTO$(11):J=1
 840 IFJ=0THEN875
 845 GOTO9
 850 PRINT"TRIDENT MISSILES!":PRINT"MISSILE loading hatch":GOTO9
 855 IFNOTDTHEN875
 860 IFPEEK(A+6)>49THENPOKEA+6,56:PRINTO$(9):GOTO9
 865 GOTO875
 870 IFNOTETHENPRINT"TORPEDOES":GOTO9
 875 PRINT"Nothing Special":GOTO9
 880 PRINT"ITS NOT HERE!":GOTO9
 885 PRINT"No RESPONSE-  Ship's":PRINT:PRINT"Computer not FUNCTIONING
 890 GOTO9
 895 PRINT"YOU CAN'T DO THAT!":GOTO9
 900 PRINT"OH NO! You spilled ACID":PRINT"all over yourself!":GOTO935
 905 PRINT"OK":GOTO9
 910 FORI=0TO15:PRINTTAB(RND(1)*20)"ZAP!":NEXT:GOTO935
 915 PRINT"PIPES dripping cold":PRINT"Sea-water":GOTO9
 920 PRINT:PRINT"No effect!":GOTO9
 925 PRINT"Core temp critical!":PRINT:PRINT"Meltdown iminent!
 930 GOSUB990:GOSUB990:FORI=0TO45:PRINTTAB(RND(1)*21)"BOOM":NEXT
 935 PRINT:PRINT"YOU'RE DEAD!":GOTO1005
 940 PRINT:PRINT"DON'T HAVE THAT":GOTO9
 945 PRINT"OH NO! you're DROWNING!!":GOSUB990:GOSUB990
 950 FORI=0TO20:PRINTTAB(RND(1)*27)"GLUB":NEXT:GOTO935
 955 PRINT:PRINT"Sizzzle..":PRINT"That won't HELP!":GOTO9
 960 FORI=0TO20:PRINTTAB(RND(1)*27)"CHOMP":NEXT:GOTO935
 965 O$(39)="Open HATCH":GOTO265
 970 A$="F9CEAAABLAAHAABA7BAAAAKAAADABLAAAKAAJFA;IDAA":B=561
 975 GOSUB985:A$="sFFsssssHHHEsGsKBBBCCEFGIJCDDKKLLLGFB":A=B+J-1
 980 K=65:GOSUB985:RETURN
 985 FORI=1TOLEN(A$):POKEB+J,ASC(MID$(A$,I,1))-K:J=J+1:NEXT:RETURN
 990 FORI=0TO2000:NEXT:PRINT:RETURN
 995 PRINT:PRINT:PRINT"You MADE IT!":PRINT:PRINT"Don't get the bends
 1000 PRINT"on the way up!":FORT=1TO20:PRINT:FORR=1TO200:NEXTR,T
 1005 GOSUB990:POKE15,255:POKE56900,1
