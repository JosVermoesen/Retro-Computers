REM Pyramid Adventure 1st part ML load

 5 REM COPYRIGHT 1980 AARDVARK -- ILLEGAL TO COPY
 10 POKE133,250:POKE134,30:POKE11,250:POKE12,30:L=7930:K=L
 20 FORI=45383TO45644:X=PEEK(I):POKEL,X:L=L+1:NEXT
 30 FORT1=1TO13:READX,Y:POKEX,Y:NEXT
 40 DATA7964,31,7963,140,7989,31,7988,134,8014,31,8013,203,8067,31,8066
 50 DATA140,8191,30,8190,254,7997,4,8146,2,8147,24
 60 FORX=548TO768:READP:POKEX,P:NEXT
 70 PRINT"READY FOR ADVENTURE TAPE":NEW
 1000 DATA 2 , 0 , 0 , 0 , 0 , 0 , 4 , 1 , 3
 1010 DATA 3 , 0 , 0 , 3 , 3 , 2 , 2 , 0 , 0
 1020 DATA 0 , 2 , 0 , 0 , 0 , 0 , 0 , 0 , 0
 1030 DATA 0 , 0 , 0 , 0 , 0 , 0 , 0 , 0 , 5
 1040 DATA 6 , 0 , 0 , 0 , 8 , 0 , 0 , 0 , 0
 1050 DATA 0 , 0 , 7 , 0 , 0 , 0 , 0 , 0 , 8
 1060 DATA 0 , 0 , 0 , 0 , 14 , 0 , 0 , 0 , 0
 1070 DATA 0 , 0 , 0 , 11 , 0 , 0 , 0 , 0 , 0
 1080 DATA 0 , 0 , 0 , 0 , 15 , 0 , 22 , 0 , 0
 1090 DATA 0 , 0 , 0 , 28 , 0 , 0 , 0 , 0 , 0
 1100 DATA 0 , 17 , 28 , 0 , 0 , 0 , 16 , 18 , 19
 1110 DATA 0 , 0 , 0 , 0 , 0 , 17 , 19 , 0 , 0
 1120 DATA 0 , 18 , 20 , 17 , 0 , 0 , 0 , 21 , 23
 1130 DATA 19 , 0 , 0 , 20 , 23 , 22 , 0 , 0 , 0
 1140 DATA 0 , 0 , 0 , 21 , 0 , 0 , 24 , 21 , 0
 1150 DATA 20 , 0 , 0 , 25 , 23 , 0 , 0 , 0 , 0
 1160 DATA 26 , 24 , 0 , 0 , 0 , 0 , 27 , 25 , 0
 1170 DATA 0 , 0 , 0 , 0 , 26 , 0 , 28 , 0 , 0
 1180 DATA 0 , 29 , 27 , 16 , 0 , 0 , 28 , 0 , 0
 1190 DATA 0 , 0 , 0 , 1 , 1 , 3 , 4 , 30 , 31
 1200 DATA 8 , 3 , 3 , 5 , 31 , 31 , 31 , 30 , 31
 1210 DATA 18 , 31 , 31 , 30 , 5 , 31 , 31 , 4 , 17
 1220 DATA 1 , 26 , 22 , 15 , 11 , 5 , 8 , 14 , 13
 1230 DATA 31 , 79 , 65 , 84 , 44 , 65 , 78 , 67 , 73
 1240 DATA 69 , 78 , 84 , 32 , 0

RUN

LOAD

REM Pyramid Adventure --main part

 10 A=548:B=A+174:POKEA+67,0
 20 DIMD$(15),O$(39):FORX=1TO32:PRINT:NEXT
 30 FORX=1TO15:READD$(X):NEXT
 40 DATAARCHEALOGISTS HUT,PATH,DESERT,PYRAMID STEPS
 50 DATA"CAVERN - STEEP ROCK WALLS
 60 L=1:K=3:DATABOAT,ANCIENT CAVE,EMPTY TREASURE ROOM,ALTAR
 70 DATADUSTY CELLAR,THRONE ROOM,HALL OF COLUMNS,NARROW LEDGE,CRYPT
 80 DATAEMPTY ROOM - RECTANGULAR PATTERN OF LINES ON THE FLOOR
 190 W$="GOLOTADRDIPUOPLIJUBUSHREGETISCSA
 200 FORX=0TO39:READO$(X):O$=O$+LEFT$(O$(X),2)
 210 X=USR(X)
 220 NEXTX:DATANORTH,SOUTH,EAST,WEST,UP,DOWN
 230 DATAKNAPSACK,SHOVEL,AARDVARK,VINES
 240 DATATIN,MATCHES,SWORD
 250 DATAMONKEY,BANANAS,ROCKS,DAGGER - JEWELED HANDLE
 260 DATAAMULET -JEWELED AND GORGEOUS,BULLETS
 270 DATAPISTOL,KEY,SKELETON,DEATHMASK OF GOLD
 280 DATAGUNPOWDER,FLASHLIGHT,STREAM,SE,WA
 290 DATAHEAVY DOOR,INSCRIPTION,SIGN,GRAFITTI
 300 DATAMUMMY - WON'T LET ME PASS,HOLE-VERY SMALL-IN FLOOR
 310 DATAKILLER ANTS,BOAT IN THE SAND,ALTAR
 320 GOTO580:DATASARCOPHAGUS,CHASM-WIDE AND SURROUNDING ME,AS
 330 INPUT"TELL ME WHAT TO DO";A$:B$="":S=99
 335 IFA$="STOP"THENSTOP
 340 IFS<>99THENIFLEN(A$)>1THENB$=LEFT$(A$,2)
 350 FORX=1TOLEN(A$)
 360 IFMID$(A$,X,1)=" "ANDLEN(A$)>X+1THENB$=MID$(A$,X+1,2)
 370 NEXT:F=0
 380 IFLEFT$(A$,3)="INV"THEN1230
 390 IFLEN(A$)>1THENA$=LEFT$(A$,2)
 400 FORX=1TOLEN(O$)STEP2:IFMID$(O$,X,2)=B$THENS=((X+1)/2)-1
 410 NEXT:X=USR(X):RETURN
 420 IFPEEK(B+2)<>11ORPEEK(B+28)<>11THEN430
 425 PRINT"GOBBLE":POKEB+28,31:POKEA+61,12
 430 IFL=4ORL=12THENPOKEB+22,L
 435 IFRND(8)<.8THEN450
 440 E=PEEK(B+8):POKEB+7,E:IFE=0ORE=30THENPOKEB+7,L
 450 PRINT:PRINT:GOSUB330:IFWTHENIFL=5THEN1530
 460 FORX=1TOLEN(W$)STEP2:IFMID$(W$,X,2)=A$THENF=(X+1)/2
 470 NEXT:PRINT:PRINT:X=USR(X)
 480 IFS=32ANDF<>10THENO$(32)="VERY MAD "+O$(32):GOTO420
 485 POKEB+22,4:IFL=12THENPOKEB+22,12
 490 ONFGOTO530,830,680,1160,1380,1010,1250,1190,1430,1500,520,830,680
 500 IFF>13THENONF-13GOTO790,1580,1270
 510 PRINT"I DON'T KNOW HOW":GOTO420
 520 PRINT"NO EFFECT":GOTO420
 530 IFS<6THENIFPEEK(A-6+L*6+S)THENL=PEEK(A-6+L*6+S)
 540 IFS=25THENPRINT"FLOWING TOO FAST":GOTO420
 545 IFS=37THENIFFATHENL=10:GOTO580
 550 IFS=36AND(L=8ORL=16)THENL=L+1:IFL=17THENL=10
 560 IFS=38ANDL=13THENPRINT"TOO STEEP FOR ME"
 570 IFL=5ANDS=35THENL=6:GOTO580
 580 IFO=1AND(PEEK(B+18)=0ORPEEK(B+18)=L)THEN600
 590 IFL>6THENPRINT"TOO DARK. I CAN'T SEE":GOTO420
 600 IFL=26ORL=24THENPRINT"FLOOR FEELS FUNNY"
 610 IFL>15THENPRINT"I AM IN TWISTING CORRIDOR":GOTO630
 620 PRINT"I AM IN "D$(L) :PRINT
 630 Y=1:PRINT"I SEE":FORX=0TO33
 635 IFPEEK(B+X)=LTHENPRINTO$(X+6):Y=1
 640 NEXT:PRINT:IFYTHENPRINT"NOTHING"
 645 IFL=11THENPRINT"CHASM"
 650 IFL=25THENFORX=1TO50:PRINT"I'M FALLING":NEXT:PRINT"I'M DEAD":END
 660 PRINT"OBVIOUS EXITS ARE ":FORX=0TO5
 665 IFPEEK(A-6+L*6+X)THENPRINTO$(X)" ";
 670 NEXT:PRINT:GOTO420
 680 IFS=35ANDL=5THENPRINT"MUCH TOO HEAVY"
 690 IF(S>25ANDS<>39)ORS<6THENPRINT"CAN'T":GOTO420
 695 IFS=22ANDPEEK(B+16)=LTHENFA=1
 700 IFC>3THENPRINT"HANDS FULL!":GOTO420
 710 IFS<>39ORPEEK(B+11)<>31ORL<>22THEN720
 715 POKEB+11,L:PRINT"TINKLE":GOTO420
 720 IFS=13THENPRINT"HE WONT LET ME":GOTO420
 730 IFPEEK(S-6+B)=30ORPEEK(S-6+B)=LTHEN740
 735 PRINT"DON'T SEE IT HERE":GOTO420
 740 IFPEEK(S-6+B)=30ANDPEEK(B)<>0THENPRINT"NOT HERE":GOTO420
 750 IFS=13THENPRINTO$(13)"WONT LET ME":GOTO420
 760 IFS=21ANDPEEK(B+14)=31THENPRINT"TINKLE":POKEB+14,18
 770 IFPEEK(S-6+B)=30THENK=K-1
 780 POKEB+S-6,0:C=C+1:GOTO420
 790 IFS<>9THENPRINT"CAN'T":GOTO420
 800 INPUT"TO WHAT";A$:GOSUB340
 810 IFS<>14ORPEEK(B+14)<>0THENPRINT"NOT HERE":GOTO420
 820 O$(9)=O$(9)+" TIED TO "+O$(14):GOTO420
 830 IFS=99THEN580
 840 PRINT"I SEE":IFL=5ANDS=4THENPRINTD$(7)" HIGH UP":GOTO420
 850 IFS=33ANDL=15THENPRINT"A LOCK MECHANISM!":GOTO420
 860 IFS=6ANDPEEK(B)=0THEN990
 870 IFS=3ANDL=21THENPRINTO$(14)
 880 IFS<6THENIFPEEK(A+L*6-6+S)>15THENPRINT"WINDING CORRIDORS":GOTO420
 890 IFS<6THENIFPEEK(A+L*6-6+S)THENPRINTD$(PEEK(A+L*6-6+S)):GOTO420
 900 IFS=39ANDL=22ANDASC(O$(32))=65THENPRINT"SOMETHING GLEAMS":GOTO420
 910 IFS=12ANDPEEK(B+6)=0THENPRINT"PROP. OF ALI BABBA":GOTO420
 920 IFS=25ANDL=5THENPRINT"FLOWS INTO HOLE IN WALL":GOTO420
 930 IFS<>38ORPEEK(B+10)<>31ORL<>13THEN940
 935 PRINTO$(16);D$(11)" BEYOND":GOTO420
 940 IFS=37ANDL=14THENPRINT"CAT=DBU":GOTO420
 950 IFS=29ANDL=17THENPRINT"XBMMT IBWF TFDSFUT":GOTO420
 960 IFS=31ANDL=26THENPRINT"UISFF USFBTVSFT IFSF":GOTO420
 970 IFS=30ANDL=1THENPRINT"BRING TREASURE HERE.  TYPE 'SCORE'":GOTO420
 980 PRINT"NOTHING SPECIAL":GOTO420
 990 FORX=1TO30:IFPEEK(X+B)=30THENPRINTO$(X+6)
 1000 NEXT:GOTO420
 1010 IFS<6ORS>40THENPRINT"WHAT?":GOTO420
 1020 IFS=27ANDL<>29THENPRINT"NOTHING YET":GOTO420
 1030 IFS=27THENPRINT"IT SWINGS!":POKEA+28*6+1,15:GOTO420
 1040 IFPEEK(B+S-6)<>0THENPRINT"DON'T HAVE IT":GOTO420
 1050 INPUT"WHERE";A$:IFS=6THENPOKEB,L:GOTO420
 1060 Y=S:GOSUB340:IFS<>6ORPEEK(B)<>0ORK>7THEN1070
 1065 POKEB+Y-6,30:K=K+1:C=C-1:GOTO420
 1070 IFY=15AND(S=25ORB$="HO")THENW=1:C=C-1:POKEB+9,31:GOTO420
 1080 IFS=10ANDPEEK(B+4)=0ANDPEEK(B+5)=0THENPOKEB+5,31:C=C-1:GOTO420
 1090 IFS=38ANDBAND(Y=9ORY=14)THENC=C-1:GOTO1560
 1100 IFS<>33THENS=Y:GOTO1160
 1110 IFY<>23ANDY<>11THENPRINT"WON'T FIT":GOTO420
 1120 IFY=23THENP=P+1:POKEB+17,31
 1130 IFY=11THENP=P+10:POKEB+17,31
 1140 IFP>10THENPRINT"EXPLOSION":POKEA+14*6+5,13:O$(33)="BIG HOLE
 1150 C=C-1:GOTO420
 1160 IFS<6ORS>35THENPRINT"HUH?":GOTO420
 1170 IFPEEK(B+S-6)=0THENPOKEB+S-6,L:C=C-1:GOTO420
 1180 PRINT"DONT HAVE ONE":GOTO420
 1190 IFS=24ANDPEEK(B+18)=0THENO=1
 1200 IFS=11ANDS(5)=0THENM=1
 1210 IFS=23ANDM=1ANDP=1THENP=11:GOTO1140
 1220 GOTO420
 1230 PRINT"I'M CARRYING":FORX=0TO33:IFPEEK(B+X)=0THENPRINTO$(X+6)
 1240 NEXT:GOTO420
 1250 IFS>37ORS<5THENPRINT"HUH??":GOTO420
 1260 IFS=6THENPRINT"O.K":GOTO420
 1270 IFS=26ANDL=9THENPRINT"DIZZY":L=16:GOTO420
 1280 IFS=26ANDL=16THENPRINT"OOPS":L=9:GOTO420
 1290 IFS=5AND(L<>12ORPEEK(B+14)<>0)THENPRINT"LOCKED":GOTO420
 1300 IFS=26THENPRINT"NOTHING":GOTO420
 1310 IFS=5THENO$(28)="OPEN DOOR":POKEA+67,4:POKEA+18,12:GOTO420
 1320 IFS<>37ORL<>14ORPEEK(B+16)<>31THEN1340
 1325 POKEB+16,14:O$(S)="OPEN "+O$(S):GOTO420
 1330 IFS<40THENIFPEEK(B+S-6)<>0THENPRINT"DON'T HAVE IT":GOTO420
 1340 IFS=19THENPRINT"AHA!":PRINTO$(18):POKEB+12,L:GOTO420
 1350 IFS=18THENPRINTO$(23)"!":POKEB+17,L:GOTO420
 1360 IFS=10ANDPEEK(B+4)=0ANDPEEK(B+11)=31THENPOKEB+5,L:GOTO420
 1370 PRINT"CAN'T":GOTO420
 1380 IFL=2ANDPEEK(B+1)=0THENPRINT"FELL THROUGH":L=5:GOTO580
 1390 IFL=3THENPRINT"NOTHING BUT SAND":GOTO580
 1400 PRINT"TOO HARD":GOTO580
 1410 FORX=1TO30:IFPEEK(B+X)=30THENPRINTO$(X+6)
 1420 NEXT:GOTO420
 1430 IFS<>38OR(L<>13ANDL<>11)THENPRINT"NO":GOTO420
 1440 IFC+K<4THEN1480
 1450 IFC<4ANDPEEK(B)<>0THEN1480
 1460 FORX=1TO30:PRINTTAB(X)"AAAAAARRRGGGGGG":FORY=1TO10*X:NEXTY,X
 1470 PRINT"I WAS TOO HEAVY AND DIED":END
 1480 PRINT"HERE GOES":L=L+2:IFL=15THENL=11
 1490 GOTO580
 1500 IFS<>32THENPRINT"WON'T BURN":GOTO420
 1510 IFPEEK(B+5)=0THENO$(32)="ASHES":POKEA+21*6+1,14
 1520 GOTO420
 1530 PRINT:PRINT"WATER IS RISING - I CAN'T SWIM":W=W+1
 1540 IFW=4THENPRINT"DROWNED":END
 1550 POKEA+31,7:POKEA+35,0:D$(6)="BOAT IN A LAKE":GOTO460
 1560 PRINTO$(13)" CLIMBS INTO "O$(38)" AFTER "O$(14)
 1570 PRINTO$(13)" BRINGS BACK "O$(16):POKEB+10,L:GOTO420
 1580 IFL<>1THENPRINT"NOT HERE":GOTO420
 1590 SC=0:IFPEEK(B+11)=0THENSC=SC+1
 1600 IFPEEK(B+10)=0THENSC=SC+1
 1610 IFPEEK(B+6)=0THENSC=SC+1
 1620 IFPEEK(B+16)=0THENSC=SC+1
 1630 PRINT"YOU HAVE "SC" OUT OF 4 TREASURES- SCORE = "SC*25
 1640 IFSC<4THEN420

