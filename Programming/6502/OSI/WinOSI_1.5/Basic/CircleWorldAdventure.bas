
REM CIRCLE WORLD ADVENTURE SOLVABLE?

 5 FORX=1TO32:PRINT:NEXT
 10 DIMD$(44),O$(62),L(56),P(44,6):L=17
 11 K1=245-INT(10*RND(8))
 17 T=250
 50 FORX=1TO44:READD$(X):NEXT
 55 DATARIMWALL,OPEN PLAIN,DARK FOREST,FIELD,MOUNTAIN,MOUNTAIN TOP
 57 DATALAKESHORE,SECRET TUNNEL,STEPPING DISC,SANDY BEACH,MARS ISLAND
 59 DATALAKE BOTTOM,SCRITH CAVERN,OBSERVATION DECK,CONTROL ROOM
 61 DATAMACHINE CITY,LIBRARY,VOLCANO PIT,RIMWALL,TOOL SHED
 62 DATAGREAT CHASM(EAST),CELLAR,GREAT CHASM(WEST),ROCKY CLIFF
 65 DATAANCIENT TEMPLE,ALCOVE,RIMWALL,KZINS' DEN,PATH,DRY RIVER BED
 66 DATACAVE,LABORATORY,SMALL ROOM,SUNFLOWER PATCH,JUNGLE,STAIRCASE
 67 DATATWISTY PASSAGE,ROUND ROOM,BRIDGE,TWISTY PASSAGE,TWISTY PASSAGE
 69 DATAVERTICAL SHAFT,TWISTY PASSAGE,JUNGLE
 100 FORX=1TO60:READO$(X):O$=O$+LEFT$(O$(X),2)
 101 IFX>6THENREADL(X-6)
 102 NEXT
 110 DATAEAST,WEST,NORTH,SOUTH,UP,DOWN,LASER/FLASHLIGHT,5,KEY,24
 112 DATAGOLD WIRE,19,SWITCH,26,NIPWEED,1,PARACHUTE,90,BUCKET,10
 113 DATAMOTOR,32,YELLOW BOOK,17,BLUE BOOK,17,OIL,16,WRENCH,90
 114 DATADYNAMITE,22,SAND,90,SCUBA GEAR,90,RADAR/SONAR UNIT,26
 115 DATACLOTH MASK,32,ROPE,5,MATCHES,17,ID CARD,90,GENERATOR,22
 117 DATASILVER BOX,22,GREEN BOOK,25,SHOVEL,5,PLAQUE,18,DAGGER,5
 118 DATATORCH,90,CANDELS,25,#2 RAMJET,1,#3 RAMJET,19,HOLE,6
 120 DATAPOST,6,HATCH,13,JAMMED HATCH,15,MIRROR,25,LEVER,15
 121 DATACONTROL PANEL,15,SUPERGRID CONNECTIONS,15
 122 DATAVIEWSCREEN(IN FLOOR),14,CRT READOUT,25,PRAYER WALL,25
 123 DATAAIRLOCK,90,FROZEN WATERFALL,30,TAG,15,KZIN(ASLEEP),90
 124 DATASMOOTH IRON DOOR,32,PIT,38,STEEL BEAM,38,TREE OF LIFE,13
 126 DATA#1 RAMJET,27,LOOKED DOOR,20,ALTER,25,BOLTS,32,IVORY STATUE,17
 150 FORY=1TO44:FORX=1TO6:READP(Y,X):NEXTX,Y
 155 DATA1,1,2,,,,10,4,3,1,,,,,,,,,2,5,3,,,,4,,5,28,6,,,,25,,,5
 156 DATA16,12,7,7,,,24,,,,,18,,,,,,,12,2,10,10,,,12,12,29,,,12,7,10
 157 DATA12,12,11,,,,,,42,,,,,,15,,,,,,,14,35,7,19,20,,,34,23,
 158 DATA27,,22,5,,,,,,19,19,,16,,,,,16,,,,,44,21,21,,,,,,,17,
 159 DATA17,,23,23,,,,34,24,24,,,,,,6,,5,,25,,,,,27,27,17,,,
 160 DATA,,5,,,,,,30,11,,,,,,29,,,32,33,,30,,,,31,,,,,31,,,,36,
 162 DATA,17,,,,,35,35,44,16,,,,,,,37,33,37,,40,36,,,,43,,,,
 164 DATA23,21,,,,,41,0,40,37,,,40,,43,41,,,,,,,12,13,41,,43,38,,
 165 DATA35,21,44,44,,
 180 W$="GOLOREUNPUOPBLLITUTIDRTHTO":GOTO710
 200 IFL(22)<>0THENPRINT"YOU DON'T HAVE THAT":GOTO500
 205 PRINT:INPUT"WHICH ONE";C$:IFLEN(C$)>1THENC$=LEFT$(C$,2)
 210 IFC$="RE"ANDL=21ORC$="RE"ANDL=23THEN230
 215 IFL=24ANDC$="GR"THEN240
 220 PRINT"NO EFFECT HERE":GOTO500
 230 PRINT"A FRAIL MAGNETIC BRIDGE APPEARS"
 235 P(21,1)=39:P(23,2)=39:GOTO500
 240 IFD2=0THENP(24,1)=8:D2=1:PRINT"DOOR OPENS":GOTO500
 300 IFP(25,1)=26THEN500
 305 IFL=25THENPRINT"MIRROR SWINGS OPEN":P(25,1)=26:GOTO500
 310 PRINT"NO EFFECT HERE":GOTO500
 480 PRINT"TANJ!!-BRIDGE COLLAPSES UNDER THE WEIGHT"
 481 PRINT"YOU FALL TO YOUR DEATH":END
 490 PRINT"LIGHT OFF":IFL(6)=3THENL(3)=90
 500 PRINT:INPUT"YOUR COMMAND";A$:PRINT:B$="":FORX=1TOLEN(A$)
 501 IFMID$(A$,X,1)=" "ANDLEN(A$)>X+1THENB$=MID$(A$,X+1,2)
 502 NEXT:IFA$="LUWEEWU"THEN300
 504 F=0:S=0:T=T-.5:IFT<K1THEN2500
 506 IFD1-1=TTHEN1520
 508 IFLEN(A$)>1THENA$=LEFT$(A$,2)
 509 IFA$="LO"ANDB$=""THEN710
 510 IFA$="TU"ANDB$="KN"THEN200
 511 IFB$="HO"THENB$="DO"
 512 IFB$="WI"THENB$="GO"
 515 IFA$="RE"ANDB$="CR"ORA$="LO"ANDB$="CR"THEN1005
 516 IFA$="DI"THEN1200
 517 IFA$="LO"ANDB$="HE"ORA$="RE"ANDB$="HE"THEN530
 520 IFA$="PR"THEN3200
 525 GOTO555
 530 IFL=25THENPRINT"*UW SIUOL*":GOTO500
 535 PRINT"CAN'T":GOTO500
 555 IFT<0THENPRINT"CIRCLEWORLD HAS COLLIDED WITH THE SUN":END
 560 IFT<10THENPRINT"COLLISION IMMENENT"
 600 FORX=1TOLEN(W$)STEP2:IFMID$(W$,X,2)=A$THENF=(X+1)/2
 601 NEXT:FORX=1TO120STEP2:IFMID$(O$,X,2)=B$THENS=(X+1)/2
 604 NEXT:Z=(S-6):IFA$="TA"ORA$="GE"THEN900
 605 IFA$="IN"THEN1000
 608 IFB$="FL"ANDL(1)<>0THENPRINT"DON'T HAVE IT":GOTO500
 609 IFA$="ON"ANDB$="FL"THENL3=1:PRINT"LIGHT ON":L(6)=3:GOTO500
 611 IFA$="OF"ANDB$="FL"THEN490
 613 IFL<15ORL>18THEN615
 614 IFF=1ANDB$="DI"THEN625
 615 IFL<>9THEN648
 616 IFA$="OF"ANDB$="DI"THENL=A:GOTO710
 617 IFA$="EN"ANDB$="DI"ANDL(20)=0THEN619
 618 GOTO649
 619 Z=INT(3.5*RND(8))+15:IFZ=ATHEN619
 620 PRINTCHR$(30):POKE56832,3:FORX=1TO20:PRINTSPC(2*X)"ZZZAAAPPPP!!"
 621 POKE57089,INT(140-6*X)
 622 NEXT:PRINTD$(Z):POKE56832,1:A=Z:GOTO710
 625 IFL(21)=0ORL(3)=0THENPRINT"CAN'T TRANSPORT WITH THAT":GOTO500
 626 A=L:L=9:GOTO710
 648 IFB$="DI"THENPRINT"ITS NOT HERE":GOTO500
 649 IFF=0ORS=0THENPRINT"CAN'T DO THAT":GOTO500
 650 IFF>9THEN664
 652 IFS<7THEN660
 655 IFL(Z)<>LANDL(Z)<>0THENPRINT"THAT'S NOT HERE":GOTO500
 660 ONF-1GOTO700,1030,1230,2000,2100,3300,1500,3500
 662 GOTO680
 664 IFS<7THEN670
 665 IFL(Z)<>0THENPRINT"DON'T HAVE IT":GOTO500
 670 ONF-9GOTO1350,1110,3000,3000
 680 IFS>6THENPRINT"CAN'T GO TO THAT":GOTO500
 684 IFL<>3THEN688
 685 L=INT(10*RND(8))+2:IFL>5THENL=3
 686 GOTO710
 688 IFP(L,S)=0THENPRINT"CAN'T GO THAT WAY":GOTO500
 690 IFP(L,S)>0THENL=P(L,S):GOTO710
 700 IFS>6THEN702
 701 IFP(L,S)<>0THENPRINTD$(P(L,S)):GOTO500
 702 IFL=14ANDS=45THEN709
 703 IFL=25ANDS=41THEN706
 704 IFS=28THENPRINT"RED, GREEN & BLUE KNOBS"
 705 GOTO1031
 706 PRINT"AN UGLY CREATURE WEARING A HELMET IS LOOKING BACK AT YOU"
 707 GOTO500
 709 PRINT"YOU SEE AN EMPTY SPACE AND A LOT OF STARS":GOTO500
 710 PRINT"YOU ARE AT:";D$(L)
 715 IFL=24ANDD2=0THENPRINT"THERE IS THE OUTLINE OF A DOOR IN THE ROCK"
 716 IFL=32THENPRINT"THERE IS A HOLE IN THE CIELING"
 721 IFL=42ANDL(17)<>0THENPRINT"YOU DETECT AN ODOR"
 725 IFL=34ANDK3=0THEN6010
 730 IFL(16)=0THENL(42)=12:P(12,6)=42
 731 IFL(16)<>0THENL(42)=90:P(12,6)=0
 760 IFL=39ANDL(21)=0THEN480
 850 PRINT"YOU SEE-":FORX=1TO54:IFL(X)=LTHENPRINTO$(X+6):Y=Y+1
 851 NEXTX:IFL>14ANDL<19THENGOSUB6000
 852 IFY=0THENPRINT"NOTHING"
 853 Y=0
 855 IFL=39ANDL(21)=0THEN480
 857 IFL=18ANDL(6)<>0THENPRINT"YOU FELL TO YOUR DEATH":END
 858 IFL=13ANDL(17)<>0THENPRINT"THE TREE VAPORS KILLED YOU":END
 859 IFL=12ANDL(15)<>0THENPRINT"YOU DROWN":END
 860 PRINT"OBVIOUS EXIT ARE-":PRINT:FORX=1TO6
 861 IFP(L,X)>0THENPRINTO$(X)
 862 NEXT:GOTO500
 900 IFS<6THENPRINT"WHAT??":GOTO500
 902 IFL(Z)=0THENPRINT"YOU HAVE THAT":GOTO500
 910 IFL(Z)<>LTHENPRINT"ITS NOT HERE!":GOTO500
 913 IFL=32ANDS=24THENPRINT"CAN'T":GOTO500
 915 IFS>34THENPRINT"CAN'T TAKE THAT":GOTO500
 920 IF C>9THENPRINT"YOUR ARMS ARE FULL!":GOTO500
 921 IFS=17THENPRINT:INPUT"IN WHAT";C$:GOTO950
 925 IFF7=0ANDS=14THENPRINT"IT'S BOLTED DOWN":GOTO500
 930 L(Z)=0:C=C+1:IFL=28ANDL(45)=28ANDL(5)=0THEN2525
 935 IFL=32ANDS=14THENP(32,2)=0:P(31,1)=0:PRINT"DOOR SLAMS SHUT"
 940 GOTO1000
 950 IFLEFT$(C$,2)="BU"ANDL(7)=0THEN930
 955 IFL(7)<>0THENPRINT"DON'T HAVE IT":GOTO500
 957 PRINT"CAN'T":GOTO500
 1000 PRINT"YOU ARE CARRYING-":FORX=1TO41
 1001 IFL(X)=0THENPRINTO$(X+6)
 1002 NEXT:IFC=0THENPRINT"NOTHING""
 1003 GOTO500
 1005 IFL=25ORL>14ANDL<19THEN1014
 1010 PRINT"NOT HERE":GOTO500
 1014 IFQ=0ORR=0THEN1022
 1016 POKE56832,0:PRINTCHR$(30):FORX=1TO10:PRINT:NEXT
 1020 PRINT"  CIRCLEWOLD SAVED":END
 1022 PRINT"ORBIT DECAYING-"T;"FALANS UNTIL IMPACT WITH SUN!"
 1023 IFQ=0THENPRINT"SUPERGRIND OFF"
 1024 IFR=0THENPRINT"STABILIZING RAMJETS OFF"
 1025 IFR=1THENPRINT"RAMJETS ON"
 1026 IFQ=1THENPRINT"SUPERGRID ON"
 1027 GOTO500
 1030 IFS<7THENPRINT"CAN'T"
 1031 IFS=29THENPRINT"The LIGHT in the FOREST"
 1032 IFS=47THENPRINT"Your PRAYER Will Be ANSWERED"
 1033 IFS=31THEN1070
 1034 IFS=15THEN1080
 1035 IFS=16THEN1095
 1036 IFS=7THENPRINT"ON/OFF FLIASHLIGHT OR BLAST"
 1038 IFS=60THENPRINT:PRINT"**LUWEEWU**"
 1040 IFS=25THENPRINT"PAK FUEL OIL CO."
 1041 IFS=50THENPRINT"CONNECT POWER HERE"
 1042 IFS=23THENPRINT"PROTECTIVE MASK"
 1050 PRINT:PRINT:GOTO500
 1070 PRINT"STEPPING DISC COMMANDS":PRINT"-GO DISC"
 1071 PRINT"-OFF DISC":PRINT"ENERGIZE DISC(IDREQUIRED)":GOTO500
 1080 PRINT"POWER SOURCE=MOTO+GENERATOR+WIRE+SWITCH+FUEL":GOTO500
 1095 PRINT"REFLECTIONS OF A STAR TRAVELER-"
 1097 PRINT:PRINT"A HERO'S NAME IS THE KEY"
 1098 PRINT"SPEAK IT OUT, I TO ME":GOTO500
 1100 IFP(25,1)=26THENPRINT"MIRROR CLOSES":P(25,1)=0:GOTO500
 1105 IFS=41ANDL=25THENPRINT"MIRROR SWINGS OPEN":P(25,1)=26:GOTO500
 1110 IFS<7THENPRINT"WHAT??":GOTO500
 1115 IFS=13ANDL(11)=0THENL(11)=L:C=C-1
 1120 L(Z)=L:C=C-1:PRINT"OK":GOTO500
 1200 IFL(24)<>0THENPRINT"CAN'T":GOTO500
 1201 IFL<>10THENPRINT"NOTHING HAPPENS HERE":GOTO500
 1202 PRINT"YOU ARE DIGGING A HOLE":H=H+1:L(14)=10
 1205 IFH=3THENPRINT"AHA!!":L(20)=10
 1210 IFH>3THENPRINT"TANJ-YOU HAVE DUG THROUGH AND DIE IN SPACE":END
 1220 GOTO500
 1230 IFL(2)<>0THENPRINT"NO KEY":GOTO500
 1233 IFL=20ANDS=57THEN1245
 1235 IFL=20ANDQ7=1THENPRINT"IT'S UNLOCKED!":GOTO500
 1240 PRINT"CAN'T UNLOCK THAT":GOTO500
 1245 PRINT"CLICK":Q7=1:L(15)=20:L(12)=20:O$(57)="OPEN DOOR"
 1250 L(27)=20:GOTO500
 1260 GOTO 500
 1350 IFS<>24THENPRINT"CAN'T TIE THAT":GOTO500
 1360 PRINT:INPUT"TO WHAT";C$:C$=LEFT$(C$,2)
 1365 IFL=6ANDC$="PO"THEN1390
 1366 IFL=38ANDC$="BE"THEN1392
 1370 PRINT"CAN'T":GOTO500
 1390 L(18)=6:PRINT"ROPE UNCOILS AND IS 30 FEET FROM FLOOR OF PIT"
 1391 P(6,6)=18:C=C-1:GOTO500
 1392 PRINT"ROPE UNCOILS-GOES DOWN PIT":P(32,5)=38:L(18)=32
 1394 C=C-1:GOTO500
 1500 IFL(19)<>0THENPRINT"CAN'T":GOTO500
 1504 IFS=34THENC1=1:PRINT"CANDELS ARE LIT":GOTO500
 1505 IFS<>19THENPRINT"CAN'T LIGHT THAT":GOTO500
 1510 D1=T:PRINT"THE FUSE IS LIT!":GOTO500
 1520 IFL=15ORL=14THEN1535
 1525 IFL(13)=0ORL(13)=LTHENPRINT"YOU BLEW YOURSELF UP!":END
 1530 D1=0:PRINT"THERE IS AN EXPLOSION NEARBY":L(13)=90:GOTO500
 1535 IFL=15ORL=14THENPRINT"THERE IS AN EXPLOSION"
 1536 PRINT"WATER RUSHES IN FROM ABOVE-YOU DROWN":END
 2000 IFS=10ANDL=15THEN2050
 2001 IFS<>42THENPRINT"WHY PULL A "O$(S)"?":GOTO500
 2005 IFL(9)=15ANDL(10)=15ANDL(23)=15THEN2010
 2007 PRINT"YOU AREN'T QUITE TALL ENOUGH":GOTO500
 2010 IFR=0THENR=1:T=T+100:PRINT"RAMJETS ON!":GOTO500
 2020 GOTO500
 2050 IFL(21)=15ANDL(3)=15ANDL(8)=15ANDL(4)=15THEN2060
 2055 PRINT"NOTHING HAPPENS":GOTO500
 2060 IFL(11)=15THENPRINT"SUPERGRID IS ON":Q=1:GOTO500
 2065 PRINT"NO FUEL":GOTO500
 2100 IFL=13ANDS=39THEN2105
 2101 PRINT"CAN'T":GOTO500
 2105 P(13,6)=15:P(15,5)=13:PRINT"OK"
 2110 O$(40)="HATCH-OPEN":O$(39)="OPEN HATCH":GOTO500
 2500 C=0:IFL(5)=0THEN2510
 2505 FORX=1TO41:IFL(X)=0THENL(X)=INT(41*RND(8))
 2506 NEXT
 2507 PRINT"A CAT-LIKE KZIN HAS WANDERED THROUGH-TOOK ALL"
 2508 PRINT"YOUR POSSESIONS AND SCATTERED THEM":GOSUB6005:GOTO700
 2510 FORX=1TO41:IFL(X)=0THENL(X)=28
 2513 NEXT:PRINT"THE KZIN TAKES YOUR THINGS AND RETIRES TO HIS DEN"
 2515 L(45)=28:K1=-5:IFL(21)=28THENL(21)=22
 2520 GOTO710
 2525 PRINT"THE KZIN WAKES UP AND CLAWS YOU TO DEATH":END
 3000 IFS<7THENPRINT"WHAT?":GOTO500
 3001 IFS<>9ANDS<>28THEN3010
 3005 IFL=34ANDK3=0THENK3=1:GOTO3015
 3010 L(Z)=L:C=C-1:PRINT"NO EFFECT":GOTO500
 3015 PRINT"THE SCAVENGER TAKES YOUR GIFT AND DISAPPEARS"
 3020 C=C-1:L(Z)=90:P(34,1)=24:GOTO500
 3200 IFL<>25THENPRINT"IN HERE?":GOTO500
 3203 PRINT:INPUT"FOR WHAT";C$
 3205 C$=LEFT$(C$,2):IFC$="GO"ANDL(3)=90THEN3220
 3210 PRINT"NOTHING HAPPENS":GOTO500
 3220 PRINT"GRANTED":L(3)=25:K3=2:GOTO500
 3300 IFL(1)<>0THENPRINT"CAN'T":GOTO500
 3303 IFL=32ORS=41THEN3320
 3305 IFL<>30THENPRINT"NO EFFECT":GOTO500
 3310 PRINT"THE WATERFALL MELTS":P(30,3)=31:L(43)=90:K6=1
 3311 D$(30)="STREAM":GOTO500
 3320 PRINT"LASER BEAM RELECTS BACK-YOU SHOT YOURSELF":END
 3500 IFL(12)<>0THENPRINT"CAN'T":GOTO500
 3510 IFF7=OTHENPRINT"OK":F7=1:GOTO500
 3520 GOTO500
 6000 PRINT"CRT READOUT & STEPPING DISC":Y=1:RETURN
 6005 K1=T-10-INT(15*RND(8)):RETURN
 6010 PRINT"A NIGHT SCAVENGER IS BLOCKING YOUR WAY"
 6011 PRINT"HE WANTS YOU TO TOSS HIM SOMETHING OF VALUE":GOTO850


