( send this file to FORTH with 0.250 sec line delay and 0.050 sec character delay)

FORTH DEFINITIONS HEX

: TEXT  HERE 40 1+ BLANKS WORD HERE PAD 40 1+ CMOVE ;

: LINE  DUP FFF0 AND 17 ?ERROR SCR @ (LINE) DROP ;

: 2DROP   DROP DROP ;

: 2DUP   OVER OVER ;

: 2SWAP   ROT >R ROT R> ;

VOCABULARY EDITOR IMMEDIATE HEX

: WHERE
    DUP B/SCR / DUP SCR ! ." SCR #" DECIMAL .
    SWAP 40 /MOD 40 * ROT BLOCK + CR 40 TYPE
    CR HERE C@ - SPACES 5E EMIT [COMPILE] EDITOR QUIT ;

EDITOR DEFINITIONS

: #LOCATE   R# @ 40 /MOD ;

: #LEAD   #LOCATE LINE SWAP ;

: #LAG   #LEAD DUP >R + 40 R> - ;

: -MOVE   LINE 40 CMOVE UPDATE ;

: H  LINE PAD 1+ 40 DUP PAD C! CMOVE ;

: E  LINE 40 BLANKS UPDATE ;

: S  DUP 1 - 0E DO I LINE I 1+ -MOVE -1 +LOOP E ;

: D  DUP H 0F DUP ROT
     DO I 1+ LINE I -MOVE LOOP E ;

: M  R# +! CR SPACE #LEAD TYPE 5F EMIT
                    #LAG TYPE #LOCATE . DROP ;

: T  DUP 40 * R# ! DUP H 0 M ;

: L  SCR @ LIST 0 M ;

: R  PAD 1+ SWAP -MOVE ;

: P  1 TEXT R ;

: I  DUP S R ;

: TOP  0 R# ! ;

: CLEAR  SCR ! 10 0 DO FORTH I EDITOR E LOOP ;

: FLUSH  [ LIMIT FIRST - B/BUF 4 + / ]
         LITERAL 0 DO 7FFF BUFFER DROP LOOP ;

: COPY  B/SCR * OFFSET @ + SWAP B/SCR * B/SCR OVER + SWAP
        DO DUP FORTH I BLOCK 2 - ! 1+ UPDATE LOOP
        DROP FLUSH ;

FORTH DEFINITIONS DECIMAL  ( finish up after entering the definitions )

