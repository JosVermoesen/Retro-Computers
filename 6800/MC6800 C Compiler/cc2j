
* code generation for assignment ops

ada
sua
mua
dia
moa
sra
sla
ana
boa
xoa
 ldu op2ptr remember op pointers
 ldy op1ptr
 pshs u,y
 lda oprtor get operator
 cmpa #ADA check if commutes
 beq 2f
 cmpa #MUA
 beq 2f
 cmpa #ANA bitwise op?
 blo 4f
2 lda 0,y check node types
 ldb 0,u
 cmpb #NNODE is it node?
 bne 4f
 cmpa #NNODE
 bne 3f if so - reverse ops
 ldd 1,y check node level
 cmpd 1,u
 bhi 4f
3 stu op1ptr reverse operands
 sty op2ptr
4 ldx codptr get code pointer
 pshs x remember position
 ldb oprtor get operator
 subb #ADA-ADD+FPP
 aslb
 clra make offset
 ldu #opgen point to table
 inc nofree
 jsr [d,u] do code gen
 clr nofree
 puls x
 stx codptr rest code pointer
 puls u,y reset operands
 stu op2ptr
 sty op1ptr
 bra asn2 do assignment

* asn code gen

asn ldu op2ptr get 2nd op
 jsr load do load code
asn2 ldb enbtyp get type
 ldu op1ptr get op1
 ldy #asntab
 jmp [b,y] do code gen

asntab fdb chasn,intasn,lnasn,fpasn,ptasn,arasn

chasn jsr ostb output 'stb'
 jsr giadr gen address
 inc ccok
 jmp rsltb
intasn jsr ostd output 'std'
 jsr giadr gen address
 inc ccok
 jmp rsltd
lnasn swi
fpasn swi
ptasn
arasn pshs u save operand
 ldu op1ptr get op1
 ldd 0,u check type
 puls u reset operand
 cmpa #NNAME name?
 bne 2f
 cmpb #REG is it register?
 beq 4f
2 ldy curadr get address reg
 jsr cmplar complete address register
 lda ar_ref,y get reference
 cmpa #UREF is it u reg?
 bne 3f
 jsr ostu output 'stu'
 bra 35f
3 jsr ostx output 'stx'
35 jsr giadr
 inc ccok
 ldy curadr get ar
 lda ar_ref,y u ref?
 cmpa #UREF
 beq 37f
 sty xcont
37 jmp ptrfin finish pointers
4 sta uflag set flag
 ldy curadr get ar
 jsr cmplar
 clr uflag reset flag
 ldy curadr get ar
 jmp ptrfin go finish


