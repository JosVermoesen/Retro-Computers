
 name Crypt

 text

**   CRYPT - ENCRYPT PASSWORD.
*
*    ENTRY (2,S)=POINTER TO 8 BYTE RESPONSE AREA
*          (4,S)=POINTER TO PASSWORD, TERMINATED BY
*                A ZERO BYTE

 global __crypt
__crypt pshs u,y
 ldd 8,s
 pshs d
 ldd #pwbuf
 pshs d
 bsr crypt do encryption
 leas 4,s clean up stack
 ldx #pwbuf
 ldu 6,s point to buffer
 ldb #8 set count
 pshs b save count
getpw4 lda 0,x get char
 anda #$f mask lo bits
 adda #'k make a letter
 sta 0,u+ save it
 lda 0,x+ get byte again
 lsra get hi 4 bits
 lsra
 lsra
 lsra
 adda #'a make a letter
 sta 0,u+ save it
 dec 0,s dec the count
 bne getpw4
 clr 0,u set null term
 puls b
 clz
 puls u,y,pc return

crypt LDX 2,S (X)=ADDRESS OF RESPONSE AREA
 LDY 4,S (Y)=ADDRESS OF PASSWORD
 LDA #251 SET LOOP COUNTER
 PSHS A
 LEAS -8,S RESERVE WORK SPACE
 CLRB (B)=INDEX/COUNTER

*   COPY PASSWORD TO RESPONSE AND WORK AREAS.

crypt1 LDA 0,Y+
 BNE crypt2 IF NOT END OF PASSWORD
 LEAY -1,Y BACK UP POINTER
crypt2 STA B,X STORE IN RESPONSE AREA
 STA B,S STORE IN WORK AREA
 INCB COUNT CHARACTER
 CMPB #8
 BLO crypt1 LOOP FOR 8 BYTES

*    TAUSWORTHE RANDOM NUMBER GENERATOR.
*    ADAPTED FROM THE ALGORITHM OF E. J. WATSON,
*    UNIVERSITY OF MANCHESTER.
*    ALGORITHM DOCUMENTED IN CACM, VOL 11, NO. 9, SEPT, 1968
*    P. 643.
*    VALUES USED: N=63, M=31
*    TO BE CONSISTENT WITH ALGORITHM, BITS NUMBERS ARE FROM
*    LEFT TO RIGHT, STARTING AT ZERO.

crypt4 LDA 4,S PUT BIT 32 IN CARRY
 LSLA
 LDD 2,S (D)=BITS 16-31
 ROLB
 ROLA (D)=BITS 17-32, CARRY=BIT 16
 EORB 7,S BITS 17-32 XOR BITS 48-63
 EORA 6,S
 STD 6,S REPLACE BITS 48-63
 EORB 3,S NEW 48-64 XOR BITS 16-31
 EORA 2,S
 STD 2,S REPLACE BITS 16-31
 LDD 0,S (D)=BITS 0-15
 ROLB
 ROLA (D)=BITS 1-16
 EORB 5,S BITS 1-16 XOR BITS 32-47
 EORA 4,S
 STD 4,S REPLACE BITS 32-47
 EORB 1,S NEW 32-47 XOR BITS 0-15
 EORA 0,S
 STD 0,S REPLACE BITS 0-15
 DEC 8,S COUNT ITERATION
 BNE crypt4 REPEAT 8 TIMES
 LDB #7 (B)=BYTE COUNTER
 ANDCC #$FE CLEAR CARRY
crypt5 LDA B,S ADD BACK ORIGINAL PASSWORD
 ADCA B,X
 STA B,X
 DECB
 BPL crypt5
 LEAS 9,S CLEAN UP STACK
 rts return

 bss

pwbuf rmb 18

 end
