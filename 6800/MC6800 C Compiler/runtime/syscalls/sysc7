* system calls - file 7

 name Syscals7

 text

 ext _errno,ret3,ret3a,ret5,ret5a

 lib sysdef

* NICE(incr)

  global _nice
_nice ldd 2,s
  sys setpr
  rts


* PAUSE()

  global _pause
_pause sys stop
  std _errno
  ldd #-1
  rts

* PIPE(fds)

  global _pipe
_pipe  sys crpipe
  bec 1f
  std _errno
  ldd #-1
  rts
1 pshs u
  ldu 2,s
  std 0,u
  stx 2,u
  ldd #0
  puls u,pc

* PROFILE(bufad, bufsiz, lowpc, scale)

  global _profil
_profil ldd 8,s
  pshs d
  ldd 6,s
  pshs d
  ldd 6,s
  pshs d
  ldd 12,s
  pshs d
  ldb #profil
  pshs b
  leax 0,s
  sys indx
  leas 9,s
  ldd #0
  rts


* SETUID(uid)

  global _setuid
_setuid  ldd 2,s
  sys suid
  bec 1f
  std _errno
  ldd #-1
  rts
1 ldd #0
  rts


* STIME(ptime)

  global _stime
_stime  ldx 2,s
  ldd 2,x
  ldx 0,x
  sys stime
  bec 1f
  std _errno
  ldd #-1
  rts
1 ldd #0
  rts

