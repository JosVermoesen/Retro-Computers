 text
 global _write
_write pshs x
 ldb 5,s get file descriptor
 lda #13 set write function code
 sta 5,s
 clra set word sized fd
 leax 5,s point to call
 sys 1 do indx sys call
 bec 2f error?
 ldd #-1 set error
2 puls x,pc return

 global _read
_read pshs x save x reg
 ldb 5,s get file desc
 lda #12 set read code
 sta 5,s
 clra
 leax 5,s point to call
 sys 1 do indx call
 bec 2f
 ldd #-1 set error
2 puls x,pc return

 global _open
_open pshs x save x reg
 ldd 4,s get name
 ldx 6,s get mode
 pshs d,x
 lda #10 set open code
 pshs a
 leax 0,s point to call
 sys 1 do indx sys call
 bec 2f
 ldd #-1 set error
2 leas 5,s clean stack
 puls x,pc return

 global _creat
_creat pshs x save x reg
 ldd 4,s get name
 ldx 6,s get mode
 pshs d,x
 lda #11 set create code
 pshs a
 leax 0,s point to call
 sys 1 do indx sys call
 bec 2f
 ldd #-1 set error
2 leas 5,s clean stack
 puls x,pc return

 global _close
_close ldd 2,s
 sys 15
 bec 2f
 ldd #-1
 rts return error
2 ldd #0 set good status
 rts return

 global _gtty
_gtty pshs x
 ldd 6,s get buffer
 pshs d
 lda #45 set ttyget call
 pshs a
 ldd 7,s get file desc
 leax 0,s point to call
 sys 1 do indx call
 bec 2f error?
 ldd #-1 set error
 bra 3f
2 ldd #0 set ok status
3 leas 3,s clean stack
 puls x,pc return

 global _stty
_stty pshs x
 ldd 6,s get buffer
 pshs d
 lda #46 set ttyset call
 pshs a
 ldd 7,s get file desc
 leax 0,s point to call
 sys 1 do indx call
 bec 2f error?
 ldd #-1 set error
 bra 3f
2 ldd #0 set ok status
3 leas 3,s clean stack
 puls x,pc return

 global _seek
_seek pshs x save x reg
 ldx 8,s get mode
 ldd 6,s get offset lo
 pshs d,x
 ldd #0 set offset hi = 0
 pshs d
 lda #14 set seek code
 pshs a
 ldd 11,s get fd
 leax 0,s point to call
 sys 1 do indx call
 leas 7,s clean stack
 bec 2f
 ldd #-1 set error
 puls x,pc return
2 ldd #0 set ok
 puls x,pc return

