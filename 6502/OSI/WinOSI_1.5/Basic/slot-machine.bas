
REM SLOT MACHINE .............. BORING .............


 10 REM SLOT MACHINE
 20 POKE 15,0
 30 PRINT "THIS IS A SIMULATED $.50 SLOT MACHINE"
 40 PRINT "WINNING COMBINATIONS ARE "
 50 PRINT "3 BARS  3 ORANGES  3 CHERRIES"
 60 PRINT "WINNING PAYOFF IS $3.00."
 70 PRINT "HOW MANY 50 CENT PIECES DO YOU"
 80 PRINT "WANT TO PLAY";
 90 INPUT M
 100 M=M*.5
 110 PRINT "YOU START WITH $";M
 120 PRINT "DO YOU WISH TO PLAY"
 130 PRINT "(TYPE 1 FOR YES -- 0 FOR NO)",
 140 INPUTA:POKE15,255
 150 IF A=0 THEN 540
 160 REM COUNTERS FOR BARS, ORANGES, CHERRIES
 170 B=0
 180 O=0
 190 C=0
 200 REM DETERMINE 3 FIGURES
 210 FOR I=1 TO 3
 220 N=INT(3*RND(1))+1
 230 IF N=2 THEN 280
 240 IF N=3 THEN 310
 250 PRINT  "----BAR----"
 260 B=B+1
 270 GOTO 330
 280 PRINT "---ORANGE---"
 290 O=O+1
 300 GOTO 330
 310 PRINT "---CHERRY---"
 320 C=C+1
 330 NEXT I
 340 REM DETERMINE IF PLAYER WON
 350 IF B =3 THEN 460
 360 IF O=3 THEN 460
 370 IF C=3 THEN 460
 380 REM
 390 PRINT "  TOO BAD --- YOU LOST"
 400 REM SUBTRACT 50 CENTS FROM M
 410 M=M-.5
 420 PRINT
 430 REM CHECK TO SEE IF PLAYER HAS ANY MONEY LEFT
 440 IF M=0 THEN 510
 450 GOTO 490
 460 PRINT " LUCKY --- YOU WON $3.00"
 470 M=M+3
 480 PRINT
 490 PRINT "YOU NOW HAVE $";M
 500 GOTO 120
 510 PRINT "YOU HAVE LOST ALL YOUR MONEY"
 520 PRINT
 530 GOTO 560
 540 PRINT "YOU ARE A VERY WISE PERSON, GAMBLING IS ILLEGAL IN"
 550 PRINT "WISCONSIN,AND THE SHERIF IS KNOCKING AT YOUR DOOR"
 560 POKE 15,255:END


