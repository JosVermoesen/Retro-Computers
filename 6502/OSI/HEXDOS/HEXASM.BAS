
 20 HEXASM VERSION 1.1 COPYRIGHT 1983 STEVEN P. HENDRIX
 50 REM LAST MOD 11 DEC 83
 60 IFPEEK(134)>128THENPOKE134,128:POKE133,0:CLEAR
 65 POKE202,32
 67 T$=""
 70 T=PEEK(121)+256*PEEK(122)+9:POKE240,255ANDT:POKE241,T/256:LZ=500
 71 POKE1261,8:LOAD!:POKE1261,80
 75 FORI=19TO90:T=PEEK(I):POKEI+580,T:NEXT
 76 IFPEEK(15)+PEEK(13)>79THENT=79-PEEK(13):POKE15,T
 80 INPUT"FILENAME";MF$:FORI=19TO90:T=PEEK(I+580):POKEI,T:NEXT
 82 FA=65:REM FILE ATTRIBUTE (32=BASIC, 65 = ASCII)
 83 IFFA<>65THEN88
 84 T=PEEK(121)+256*PEEK(122):POKET+26,44:POKET+37,201:POKET+38,3:REM EOF
 85 POKE222,255:T=T+40:FORI=1TO15
 86 T1=VAL(MID$("230244208002230245162019142255002208005234234",3*I-2,3))
 87 POKEI+T,T1:NEXT
 88 GOTO1600
 90 REM FIND T$ IN MP$(N)
 100 FORN=0TO9:IFMP$(N)<>""ANDT$<>MP$(N)THENNEXT
 110 RETURN
 120 REM FIND T$ IN OC$(M)
 130 L=0:H=210
 140 M=INT((L+H)/2):IFT$=OC$(M)THEN180
 150 IFT$<OC$(M)THENH=M:GOTO170
 160 L=M+1:M=L
 170 IFH>LTHEN140
 180 RETURN
 190 REM FIND T$ IN LB$(M)
 200 L=0:H=NL
 210 M=INT((L+H)/2):IFT$=LB$(LT(M))THEN250
 220 IFT$<LB$(LT(M))THENH=M:GOTO240
 230 L=M+1:M=L
 240 IFH>LTHEN210
 250 RETURN
 260 REM INSERT T$ IN LB$ AT M
 270 IFT$=LB$(LT(M))THENRETURN
 280 IFNL>=LZTHENPRINT"TOO MANY LABELS -- INCREASE LZ IN LINE 70":END
 290 FORI=NL-1TOMSTEP-1:LT(I+1)=LT(I):NEXT
 300 IFM<=PCTHENPC=PC+1
 310 IFM<=R6THENR6=R6+1
 320 LB$(NL)=T$+"":LV(NL)=LV(LT(PC)):LT(M)=NL:NL=NL+1
 330 RETURN
 340 REM ERROR HANDLER
 350 PS=PEEK(14):PRINT#DE,CHR$(13);CHR$(10);">>>>>>>>>>>>    ";
 360 PRINT#DE,ET$(ET);" >";T$;"< IN LINE";256*PEEK(245)+PEEK(244)
 370 POKE14,PS
 380 NE=NE+1
 390 IFT$<>""THENT=USR(-7)T$:GOTO390
 400 RETURN
 410 REM FIND T$ IN MN$(M)
 420 FORM=0TO9:IFMN$(M)<>""ANDT$<>MN$(M)THENNEXT
 430 RETURN
 440 REM SET OV TO VALUE OF OPERAND
 450 OV=0
 460 IFT$=""THENRETURN
 470 GOSUB590:OV=OV+TV
 480 T=USR(-7)T$
 490 IFT$=""THEN560
 500 IFT$="+"THEN470
 510 IFT$<>"-"THEN530
 520 GOSUB590:OV=OV-TV:GOTO480
 530 IFT$=","THEN560
 540 IFT$<>";"THENET=3:GOTO350
 550 GOTO390
 560 OV=OV-65536*INT(OV/65536)
 570 RETURN
 580 REM SET TV TO VALUE OF T$
 590 TV=0:T=USR(-7)T$:IFT=0THEN640
 600 IFASC(T$)>47ANDASC(T$)<58THENTV=VAL(T$):RETURN
 610 GOSUB200:IFLV(LT(M))=-1THENET=5:GOSUB350:TV=0:RETURN
 620 IFT$<>LB$(LT(M))THENET=4:GOSUB350:TV=0:RETURN
 630 TV=LV(LT(M)):RETURN
 640 IFT$="$"THENBS=16:GOTO710
 650 IFT$="&"THENBS=8:GOTO710
 660 IFT$="%"THENBS=2:GOTO710
 670 IFT$=""ORT$=","ORT$=";"THENTV=0:RETURN
 680 IFT$<>"'"THENET=3:GOTO350
 690 T=USR(-7)T$:IFT$>""THENTV=ASC(T$)
 700 RETURN
 710 T=USR(-7)T$:IFT$=""THENRETURN
 720 FORI=1TOLEN(T$):T=ASC(MID$(T$,I,1))
 730 TV=BS*TV+T-48+7*(T>57):NEXT
 740 RETURN
 750 REM GENERATE BYTE OF CODE & PRINT
 760 BG=BG-256*INT(BG/256)
 770 IFMS>=0THENT=LV(LT(PC))+MS:POKET-65536*INT(T/65536),BG
 780 IFDL>=0THENPRINT#DL," ";:GOSUB820
 790 LV(LT(PC))=LV(LT(PC))+1:RETURN
 800 REM PRINT BG AS 2 HEX DIGITS
 810 IFDL<0THENRETURN
 820 BG=BG-65536*INT((BG+32768)/65536)
 830 TH=BGAND255
 840 PRINT#DL,CHR$(FNA(TH/16));CHR$(FNA(THAND15));
 850 RETURN
 860 REM PRINT LINE
 870 IFDL<0THENRETURN
 880 IFPOS(.)>22THENPRINT#DL
 890 PRINT#DL," ";TAB(22);
 900 FORI=19TO90:IFPEEK(I)THENNEXT
 910 T1$=T$:T=PEEK(123)+256*PEEK(124)
 920 POKET+2,I-19:POKET+3,19:POKET+4,0 
 930 PRINT#DL,T$
 940 T$=T1$
 950 RETURN
 960 REM OPEN SOURCE FILE
 970 GY=PEEK(129)+256*PEEK(130)
 980 POKE129,GZAND255:POKE130,GZ/256
 990 PRINT:PRINTT$:LOAD*5,T$
 995 IFFA=65THENT=USR(5)+256*USR(5):PRINTT;"BYTES LONG"
 1000 POKE129,GYAND255:POKE130,GY/256
 1010 RETURN
 1020 DATA ADC,109,3,ADC#,105,2,ADC@,114,-2,ADC@Y,113,2,ADCS,117,2
 1030 DATA ADCX,125,3,ADCX@,97,2,ADCY,121,3,ADCZ,101,2
 1040 DATA AND,45,3,AND#,41,2,AND@,50,-2,AND@Y,49,2,ANDS,53,2,ANDX,61,3
 1050 DATA ANDX@,33,2,ANDY,57,3,ANDZ,37,2
 1060 DATA ASL,14,3,ASLA,10,1,ASLS,22,2,ASLX,30,3,ASLZ,6,2
 1070 DATA BBR0,15,-3,BBR1,31,-3,BBR2,47,-3,BBR3,63,-3
 1080 DATA BBR4,79,-3,BBR5,95,-3,BBR6,111,-3,BBR7,127,-3
 1090 DATA BBS0,143,-3,BBS1,159,-3,BBS2,175,-3,BBS3,191,-3
 1100 DATA BBS4,207,-3,BBS5,223,-3,BBS6,239,-3,BBS7,255,-3
 1110 DATA BCC,144,2,BCS,176,2,BEQ,240,2
 1120 DATA BIT,44,3,BIT#,136,-2,BITS,52,-2,BITX,60,-3,BITZ,36,2
 1130 DATA BMI,48,2,BNE,208,2,BPL,16,2,BRA,128,-2,BRK,0,1
 1140 DATA BVC,80,2,BVS,112,2
 1150 DATA CLC,24,1,CLD,216,1,CLI,88,1,CLV,184,1
 1160 DATA CMP,205,3,CMP#,201,2,CMP@,210,-2,CMP@Y,209,2,CMPS,213,2
 1170 DATA CMPX,221,3,CMPX@,193,2,CMPY,217,3,CMPZ,197,2
 1180 DATA CPX,236,3,CPX#,224,2,CPXZ,228,2
 1190 DATA CPY,204,3,CPY#,192,2,CPYZ,196,2
 1200 DATA DEC,206,3,DECA,58,-1,DECS,214,2,DECX,222,3,DECZ,198,2
 1210 DATA DEX,202,1,DEY,136,1
 1220 DATA EOR,77,3,EOR#,73,2,EOR@,82,-2,EOR@Y,81,2
 1230 DATA EORS,85,2,EORX,93,3,EORX@,65,2,EORY,89,3,EORZ,69,2
 1240 DATA INC,238,3,INCA,26,-1,INCS,246,2,INCX,254,3,INCZ,230,2
 1250 DATA INX,232,1,INY,200,1,JMP,76,3,JMP@,108,3,JMPX,124,-3,JSR,32,3
 1260 DATA LDA,173,3,LDA#,169,2,LDA@,178,-2,LDA@Y,177,2,LDAS,181,2
 1270 DATA LDAX,189,3,LDAX@,161,2,LDAY,185,3,LDAZ,165,2
 1280 DATA LDX,174,3,LDX#,162,2,LDXS,182,2,LDXY,190,3,LDXZ,166,2
 1290 DATA LDY,172,3,LDY#,160,2,LDYS,180,2,LDYX,188,3,LDYZ,164,2
 1300 DATA LSR,78,3,LSRA,74,1,LSRS,86,2,LSRX,94,3,LSRZ,70,2,NOP,234,1
 1310 DATA ORA,13,3,ORA#,9,2,ORA@,18,-2,ORA@Y,17,2,ORAS,21,2,ORAX,29,3
 1320 DATA ORAX@,1,2,ORAY,25,3,ORAZ,5,2
 1330 DATA PHA,72,1,PHP,8,1,PHX,218,-1,PHY,90,-1
 1340 DATA PLA,104,1,PLP,40,1,PLX,250,-1,PLY,122,-1
 1350 DATA RMB0,7,-2,RMB1,23,-2,RMB2,39,-2,RMB3,55,-2
 1360 DATA RMB4,71,-2,RMB5,87,-2,RMB6,103,-2,RMB7,119,-2
 1370 DATA ROL,46,3,ROLA,42,1,ROLS,54,2,ROLX,62,3,ROLZ,38,2
 1380 DATA ROR,110,3,RORA,106,1,RORS,118,2,RORX,126,3,RORZ,102,2 
 1390 DATA RTI,64,1,RTS,96,1
 1400 DATA SBC,237,3,SBC#,233,2,SBC@,242,-2,SBC@Y,241,2,SBCS,245,2
 1410 DATA SBCX,253,3,SBCX@,225,3,SBCY,249,3,SBCZ,229,2
 1420 DATA SEC,56,1,SED,248,1,SEI,120,1
 1430 DATA SMB0,135,-2,SMB1,151,-2,SMB2,167,-2,SMB3,183,-2
 1440 DATA SMB4,199,-2,SMB5,215,-2,SMB6,231,-2,SMB7,247,-2
 1450 DATA STA,141,3,STA@,146,-2,STA@Y,145,2,STAS,149,2,STAX,157,3
 1460 DATA STAX@,129,2,STAY,153,3,STAZ,133,2
 1470 DATA STX,142,3,STXS,150,2,STXZ,134,2
 1480 DATA STY,140,3,STYS,148,2,STYZ,132,2
 1490 DATA STZ,156,-3,STZS,116,-2,STZX,158,-3,STZZ,100,-2
 1500 DATA TAX,170,1,TAY,168,1
 1510 DATA TRB,28,-3,TRBZ,20,-2,TSB,12,-3,TSBZ,4,-3
 1520 DATA TSX,186,1,TXA,138,1,TXS,154,1,TYA,152,1
 1530 DATA ""
 1540 DATA ??????,DUPLICATE LABEL,BAD OP CODE,UNREADABLE OPERAND
 1550 DATA UNDEFINED LABEL,ILLEGAL FORWARD REFERENCE,REDEFINED MACRO
 1560 DATA ILLEGAL FORMAL PARAMETER,LABEL NOT ALLOWED,MACRO TOO LONG
 1570 DATA DIRECTIVE NOT ALLOWED,UNKNOWN DIRECTIVE,FILE NAME TOO LONG
 1580 DATA UNDEFINED OPTION,ERROR IN MACRO,BRANCH OUT OF RANGE
 1590 DATA R65C00 OPCODE,""
 1600 DIMOC$(210),OC(210),BT(210),LB$(LZ),LV(LZ),LT(LZ),MD$(10),MP$(10)
 1605 FORI=19TO90:IFPEEK(I)<>137THENNEXT
 1606 FORI=I+1TO90:IFPEEK(I)<>0ANDPEEK(I)<>58THENNEXT
 1607 IFPEEK(I)<>58THENI=-1
 1608 IFI>0THENI=I+1:IFPEEK(I)<>34THENI=-1
 1610 POKE765,5:POKE766,0:POKE767,I+1
 1620 DEFFNA(X)=48+X-7*(X>=10)
 1630 DIMPL(10),MN$(10),ET$(30)
 1640 DL=-1:DE=0:MS=-1:DS=0:DR=-1:DE=0
 1650 FORI=0TO209:READOC$(I),OC(I),BT(I):NEXT
 1660 OC$(I)=CHR$(255)
 1670 READT$:IFT$=""THEN1690
 1680 PRINT"ERROR IN HEXASM":STOP
 1690 FORI=0TO30:READET$(I):IFET$(I)<>""THENNEXT
 1700 FORI=0TOLZ:LV(I)=-1:LB$(I)="^":NEXT
 1710 GZ=PEEK(129)+256*PEEK(130)
 1720 LOAD*5,MF$:IFFA=65THENT=USR(5)+256*USR(5):PRINT:PRINTT;"BYTES LONG"
 1730 REM FIRST PASS
 1740 PC=0:POKE244,0:POKE245,0
 1750 LB$(0)="PC":LV(PC)=0:NL=1
 1760 LB$(1)="R65C00":R6=1:LT(R6)=1:LV(LT(R6))=0:NL=2
 1770 T=USR(-7)T$
 1780 IFT$=";"THENGOSUB390
 1790 PRINTCHR$(13);PEEK(244)+256*PEEK(245);:POKE14,0
 1800 IFT$=""ANDTTHEN2920
 1810 IFT$=""ORT$=";"THEN2890
 1820 IFT=0THEN1930
 1830 GOSUB130:IFT$=OC$(M)THEN1920
 1840 GOSUB200
 1850 IFM<>PCANDT$=LB$(LT(M))THENET=1:GOSUB350:GOTO1870
 1860 GOSUB270
 1870 T=USR(-7)T$
 1880 IFT$=""ORT$=";"THEN2890
 1890 IFT=0THEN1930
 1900 GOSUB130
 1910 IFT$<>OC$(M)THENET=2:GOSUB350:LV(LT(PC))=LV(LT(PC))+3:GOTO1770
 1920 LV(LT(PC))=LV(LT(PC))+ABS(BT(M)):GOTO2890
 1930 IFT$="="THENSM=M:GOSUB450:LV(LT(SM))=OV:GOTO2890
 1940 IFT$=""ORT$=";"THEN2890
 1950 IFT$<>"."THENET=2:GOSUB350:GOTO1770
 1960 T=USR(-7)T$
 1970 IFT$<>"BYTE"ANDT$<>"HBYTE"THEN2020
 1980 LV(LT(PC))=LV(LT(PC))+1
 1990 T=USR(-7)T$:IFT$=","THEN1980
 2000 IFT$<>";"ANDT$<>""THEN1990
 2010 GOTO2890
 2020 IFT$<>"WORD"ANDT$<>"DBYTE"THEN2080
 2030 LV(LT(PC))=LV(LT(PC))+2
 2040 T=USR(-7)T$
 2050 IFT$=","THEN2030
 2060 IFT$<>""THEN2040
 2070 GOTO2890
 2080 IFT$<>"STRING"THEN2130
 2090 T=USR(-7)T$:IFT$<>"'"THENET=3:GOSUB350:GOTO2900
 2100 T=PEEK(123)+256*PEEK(124):T=PEEK(T+3)
 2110 T=T+1:IFPEEK(T)>0THENLV(LT(PC))=LV(LT(PC))+1:GOTO2110
 2120 GOTO2890
 2130 IFT$<>"IF"THEN2180
 2140 GOSUB450:IFOVTHEN2890
 2150 T=USR(-7)T$:IFT$<>"."THEN2150
 2160 T=USR(-7)T$:IFT$<>"ENDIF"THEN2150
 2170 GOTO2890
 2180 IFT$<>"IFNOT"THEN2210
 2190 GOSUB450:IFOVTHEN2150
 2200 GOTO2890
 2210 IFT$="ENDIF"THEN2890
 2220 IFT$="SET"THENGOSUB2250:LV(LT(M))=32767:GOTO2890
 2230 IFT$="RESET"THENGOSUB2250:LV(LT(M))=0:GOTO2890
 2240 GOTO2270
 2250 T=USR(-7)T$:GOSUB200:IFT$<>LB$(LT(M))THENGOSUB270
 2260 RETURN
 2270 IFT$<>"INVOKE"THEN2350
 2280 T=USR(-7)T$
 2290 GOSUB420:IFT$<>MN$(M)THENET=5:GOSUB390:GOTO2890
 2300 I=1
 2310 IFI>LEN(MD$(M))THEN2890
 2320 T=ASC(MID$(MD$(M),I,1))
 2330 IFT<254THENLV(LT(PC))=LV(LT(PC))+T:I=I+T+1:GOTO2310
 2340 LV(LT(PC))=LV(LT(PC))+T-253:I=I+2:GOTO2310
 2350 IFT$<>"MACRO"THEN2740
 2360 T=USR(-7)T$:GOSUB420:IFT$<>MN$(M)THENMN$(M)=T$+"":GOTO2430
 2370 ET=6
 2380 GOSUB350
 2390 T=USR(-7)T$:IFT$<>"."THEN2390
 2400 T=USR(-7)T$:IFT$<>"ENDMACRO"THEN2390
 2410 MN$(NM)=""
 2420 GOTO2890
 2430 FORI=0TO10:MP$(I)="":NEXT
 2440 T=USR(-7)T$:IFT$=""THEN2500
 2450 IFT$=";"THEN2490
 2460 GOSUB100:IFMP$(N)<>""THENET=7:GOTO2380
 2470 MP$(N)=T$+""
 2480 T=USR(-7)T$:IFT$=","THEN2440
 2490 GOSUB390
 2500 I=0:J=1:TE=PEEK(129)+256*PEEK(130)
 2510 MD$(NM)="................................"
 2520 MD$(NM)=MD$(NM)+MD$(NM)+MD$(NM)+MD$(NM)
 2530 MD$(NM)=MD$(NM)+LEFT$(MD$(NM),127)
 2540 POKE130,INT(TE/256)-1:POKE129,TEAND255
 2550 MD$(NM)=""+MD$(NM)
 2560 T=USR(-7)T$:IFT$="."THEN2680
 2570 GOSUB130:IFT$<>OC$(M)THENET=8:GOSUB350:GOTO2380
 2580 POKETE+J-511,OC(M):JT=J+ABS(BT(M)):J=J+1
 2590 T=USR(-7)T$:IFT$=""THEN2620
 2600 IFT$<>"+"THEN2630
 2610 GOSUB450:T=INT(OV/256):POKETE+J-511,OV-256*T:POKETE+J-510,T
 2620 J=JT:GOTO2660
 2630 GOSUB100:IFT$<>MP$(N)THENET=7:GOSUB350:GOTO2560
 2640 POKETE+I-511,J-I-1:I=J:J=I+1
 2650 POKETE+I-511,252+BT(M):POKETE+J-511,N:I=I+2:J=I+1
 2660 GOSUB390:IFJ>255THENET=9:GOSUB350:I=0:J=1
 2670 GOTO2560
 2680 T=USR(-7)T$:IFT$<>"ENDMACRO"THENET=10:GOSUB350:GOTO2660
 2690 IFJ>ITHENPOKETE+I-511,J-I-1
 2700 POKE130,TE/256:POKE129,TEAND255
 2710 MD$(NM)=LEFT$(MD$(NM),J)+""
 2720 NM=NM+1
 2730 GOTO2890
 2740 IFT$="FILE"THENT=USR(-7)T$:GOSUB970:GOTO2890
 2750 IFT$="PAGE"THEN2890
 2760 IFT$<>"OPTION"THENET=11:GOSUB350:GOTO2890
 2770 GOSUB2780:GOTO2900
 2780 T=USR(-7)T$:IFT$=""THENRETURN
 2790 IFTTHENT1$=T$:GOTO2810
 2800 ET=13:GOTO350
 2810 T=USR(-7)T$:IFT$="-"ORT$=""THENOV=-1:GOTO2830
 2820 OV=VAL(T$)
 2830 IFT1$="L"THENDL=OV:IFOV=2THENNULL10
 2840 IFT1$="C"THENMS=OV
 2850 IFT1$="S"THENDS=OV
 2860 IFT1$="R"THENDR=OV
 2870 IFT1$="E"THENDE=OV
 2880 GOTO2780
 2890 GOSUB390
 2900 REM
 2910 GOTO1770
 2920 PRINT:PRINTNE;" ERRORS IN PASS #1":NE=0:PRINT
 2930 REM SECOND PASS
 2935 PG=1
 2940 T$=MF$:GOSUB970
 2950 LV(LT(PC))=0:LV(LT(R6))=0:POKE244,0:POKE245,0
 2960 T=USR(-7)T$
 2970 IFT$=";"THENGOSUB390
 2980 IFT$=""ANDTTHEN3940
 2990 IFT$=""ORT$=";"THEN3930
 3000 IFDLTHENPRINTCHR$(13);PEEK(244)+256*PEEK(245);:POKE14,0
 3010 IFDL<0THEN3050
 3020 PRINT#DL,PEEK(244)+256*PEEK(245)TAB(7);
 3030 BG=LV(LT(PC))/256:GOSUB810
 3040 BG=LV(LT(PC))-65536*INT((LV(LT(PC))+32768)/65536)-32768:GOSUB810
 3050 REM
 3060 IFTTHEN3710
 3070 IFT$=""ORT$=";"THEN3930
 3080 IFT$<>"."THENET=2:GOSUB350:GOTO3930
 3090 T=USR(-7)T$
 3100 IFT$<>"BYTE"THEN3130
 3110 GOSUB450:BG=OV:GOSUB760:IFT$=","THEN3110
 3120 GOTO3930
 3130 IFT$<>"WORD"THEN3160
 3140 GOSUB450:BG=OV:GOSUB760:BG=OV/256:GOSUB760:IFT$=","THEN3140
 3150 GOTO3930
 3160 IFT$<>"STRING"THEN3210
 3170 T=USR(-7)T$:IFT$<>"'"THENET=3:GOSUB350:GOTO3930
 3180 TP=PEEK(123)+256*PEEK(124):TP=PEEK(TP+3)
 3190 TP=TP+1:BG=PEEK(TP):IFBG>0THENGOSUB760:GOTO3190
 3200 GOTO3930
 3210 IFT$<>"HBYTE"THEN3240
 3220 GOSUB450:BG=OV/256:GOSUB760:IFT$=","THEN3220
 3230 GOTO3930
 3240 IFT$<>"DBYTE"THEN3270
 3250 GOSUB450:BG=OV/256:GOSUB760:BG=OV:GOSUB760:IFT$=","THEN3250
 3260 GOTO3930
 3270 IFT$<>"IF"THEN3340
 3280 GOSUB450:IFOVTHEN3930
 3290 GOSUB870
 3300 T=USR(-7)T$:IFT$<>"."THEN3300
 3310 T=USR(-7)T$:IFT$<>"ENDIF"THEN3300
 3320 IFDL>=0THENPRINT#DL,"--- CONDITIONAL FALSE ---"
 3330 GOTO3930
 3340 IFT$<>"IFNOT"THEN3370
 3350 GOSUB450:IFOVTHEN3290
 3360 GOTO3930
 3370 IFT$="ENDIF"THEN3930
 3380 IFT$="SET"THENGOSUB2250:LV(LT(M))=32767:GOTO3930
 3390 IFT$="RESET"THENGOSUB2250:LV(LT(M))=0:GOTO3930
 3400 IFT$<>"INVOKE"THEN3560
 3410 T=USR(-7)T$:GOSUB420:MN=M
 3420 FORI=0TO9:OV=0:GOSUB480:PV(I)=OV:IFT$=","THENNEXT
 3430 FORI=I+1TO10:PV(I)=0:NEXT
 3440 FORI=1TOLEN(MD$(MN)):N=ASC(MID$(MD$(MN),I,1))
 3450 IFN<254THEN3500
 3460 I=I+1
 3470 T=ASC(MID$(MD$(MN),I,1)):BG=PV(T):GOSUB760
 3480 IFN>254THENBG=PV(T)/256:GOSUB760
 3490 GOTO3540
 3500 IFN<1THEN3930
 3510 IFI+N>LEN(MD$(MN))THENET=14:GOSUB350:GOTO3530
 3520 FORJ=1TON:BG=ASC(MID$(MD$(MN),I+J,1)):GOSUB760:NEXT
 3530 I=I+J-1
 3540 NEXTI:IFDL>=0THENPRINT#DL
 3550 GOTO3930
 3560 IFT$<>"MACRO"THEN3630
 3570 T=USR(-7)T$:IFT$<>""THEN3600
 3580 GOSUB870:T=USR(-7)T$
 3590 IFDL>=0THENPRINT#DL,PEEK(244)+256*PEEK(245);
 3600 IFT$<>"."THEN3570
 3610 T=USR(-7)T$:IFT$<>"ENDMACRO"THEN3570
 3620 GOTO3930
 3630 IFT$="FILE"THENT=USR(-7)T$:GOSUB970:GOTO3930
 3640 IFT$="OPTION"THENGOSUB2780:GOTO3930
 3650 IFT$="PAGE"THENPG=PG+1:IFDL<0THEN3930
 3660 IFDL=0THENPRINTCHR$(26);CHR$(3);
 3670 IFDLTHENPRINT#DL,CHR$(12);
 3672 T=PEEK(123)+256*PEEK(124)+3:T=PEEK(T)+256*PEEK(T+1)
 3674 FORI=T-1TOT+3:POKEI,32:NEXT
 3680 PRINT#DL,"HEXASM ASSEMBLY LISTING      ";
 3690 PRINT#DL,"PAGE  #";PG:PRINT#DL:GOTO3930
 3700 ET=11:GOSUB350:GOTO3930
 3710 GOSUB130:IFT$=OC$(M)THEN3770
 3720 GOSUB200:IFT$<>LB$(LT(M))THENET=2:GOSUB350:GOTO3760
 3730 T=USR(-7)T$
 3740 IFT$="="THENSM=M:GOSUB450:LV(LT(SM))=OV:GOTO3930
 3750 IFT=0THEN3070
 3760 GOSUB130:IFT$<>OC$(M)THENET=2:GOSUB350:GOTO3930
 3770 IFBT(M)>0ORLV(LT(R6))THEN3810
 3780 PS=PEEK(14):PRINT#DE,CHR$(13);CHR$(10);">>>>>>>>>>>>    ";
 3790 PRINT#DE,ET$(16);" >";T$;"< IN LINE";256*PEEK(245)+PEEK(244)
 3800 POKE14,PS:NE=NE+1
 3810 TM=M:BG=OC(TM):LO=BG:GOSUB760
 3820 IFABS(BT(TM))<2THEN3930
 3830 IF(BGAND31)<>16ANDBG<>128THENGOSUB450:BG=OV:GOSUB760:GOTO3870
 3840 GOSUB450:BG=OV-LV(LT(PC))-1
 3850 IFBG<-128ORBG>127THENET=15:GOSUB350:GOTO3930
 3860 GOSUB760
 3870 IFABS(BT(TM))<3THEN3930
 3880 IF(LOAND15)<>15THENBG=OV/256:GOSUB760:GOTO3930
 3890 IFT$<>","THENET=17:GOSUB350:GOTO3930
 3900 GOSUB450:BG=OV-LV(LT(PC))-1
 3910 IFBG<-128ORBG>127THENET=15:GOSUB350:GOTO3930
 3920 GOSUB760
 3930 GOSUB870:GOSUB390:GOTO2960
 3940 PRINT:PRINTNE;" ERRORS IN PASS #2"
 3950 REM SYMBOL TABLE
 4100 IFDS<0THEN4400
 4110 LP=0:IFDS<>DLTHENPG=0:GOTO4120
 4112 IFLP>=NLTHEN4400
 4115 IFDS=0THENPRINTCHR$(26);
 4116 IFDSTHENPRINT#DS,CHR$(12);
 4120 PG=PG+1:PRINT#DS,"HEXASM ASSEMBLY LISTING       PAGE  #"PG
 4130 PRINT#DS:PRINT#DS
 4135 PRINT#DS,"SYMBOL TABLE   (PAGE"INT(LP/100)+1" OF"INT(NL/100)+1")"
 4140 PRINT#DS:PRINT#DS,"ADDR  SYMBOL"TAB(32)"ADDR  SYMBOL"
 4150 PRINT#DS
 4160 DL=DS
 4170 T=INT((NL-LP)/2+.5):IFT>50THENT=50
 4180 FORI=LPTOI+T-1
 4190 FORJ=ITOI-T*(I<LP+T-1OR((NLAND1)=0))STEPT
 4200 BG=LV(LT(J))/256:GOSUB810
 4210 BG=LV(LT(J)):GOSUB810
 4220 PRINT#DS,"  "LB$(LT(J));
 4230 PRINT#DS,TAB(32);:NEXTJ
 4240 IFPOS(0)THENPRINT#DS
 4250 NEXTI
 4260 LP=LP+100
 4270 GOTO4112
 4400 END