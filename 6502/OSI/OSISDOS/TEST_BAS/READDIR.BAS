 1 REM PROGRAM DOES NOT WORK DUE TO 16-BIT SIGNED MATHS
 2 REM SECTOR NUMBER OF THE DIRECTORY ON MY SD CARD WAS 32768
 3 REM PROGRAM CAN'T HANDLE BIT WISE MATH ON NUMBERS BIGGER THAN 32767
 100 REM OSISDOS PROTOTYPE READ SD CARD IN BASIC
 110 SAVE:REM TURN ON LOGGING TO SERIAL PORT
 120 REM SDCARD BASE ADDRESS = $F010 (61456 DEC)
 130 DT=61456:REM SDDAT
 140 ST=61457:REM SDSTATUS/SDCONTROL
 150 A0=61458:REM SDLBA0
 160 A1=61459:REM SDLBA1
 170 A2=61460:REM SDLBA2
 180 DIM MA(512):REM RESERVE ARRAY SPACE FOR SD CARD DATA
 200 REM READ THE MBR
 210 POKE A0,0
 220 POKE A1,0
 230 POKE A2,0
 240 GOSUB 1000
 245 PRINT "MASTER BOOT RECORD CONTENTS"
 250 GOSUB 1200
 260 GOSUB 1800
 270 PRINT "DS";DS
 300 REM REM READ THE DIRECTORY
 310 PV = DS AND 127:REM THIS FAILS DUE TO SIGNED 16-BIT MATHS
 312 IF DS AND 128 = 128
 314 PV = PV OR 128
 320 POKE A0,PV
 330 POKE A1,((DS/256) AND 255)
 340 POKE A2,0
 350 GOSUB 1000
 360 PRINT "MASTER BOOT RECORD CONTENTS"
 370 GOSUB 1200
 
 999 END

 1000 REM READ A SECTOR OF THE SD CARD
 1005 REM A0, A1, A2 = LBA - NEED TO BE SET PRIOR TO CALLING
 1010 SC=PEEK(ST)
 1020 REM PRINT "SD CTRLR STAT =";SC
 1030 IF SC<>128 GOTO 1010
 1040 REM WRITE 0 TO CONTROL REG FOR READ BLOCK
 1050 POKE ST,0
 1100 REM WAIT FOR READ VALUE READY
 1110 REM READ SD CARD INTO ARRAY
 1120 FOR MO=0 TO 511
 1130 SV=PEEK(ST)
 1140 IF SV <> 224 GOTO 1130
 1150 MA(MO)=PEEK(DT)
 1160 NEXT MO
 1170 RETURN

 1200 REM DUMP ARRAY TO SCREEN
 1205 REM ARRAY IS FROM THE ARRAY AT MA()
 1210 FOR I=0 TO 31
 1215 SA=(I*16)
 1220 FOR J=0 TO 15
 1230 DV=MA(SA+J)
 1240 GOSUB 1300
 1260 NEXT J
 1270 GOSUB 1500 
 1280 NEXT I
 1290 RETURN
 1300 REM PRINT DECIMAL NUMBER AS TWO HEX DIGITS
 1310 NB=(DV AND 240)/16
 1320 GOSUB 1370
 1330 NB=DV AND 15
 1340 GOSUB 1370
 1350 PRINT " ";
 1360 RETURN
 1370 REM PRINT NIBBLE
 1375 IF NB>9 GOTO 1410
 1380 VL=NB+48
 1390 PRINT CHR$(VL);
 1400 RETURN
 1410 REM CHARACTER IS LETTER A-F
 1415 VL=NB+55
 1420 PRINT CHR$(VL);
 1430 RETURN
 1500 REM PRINT THE ASCII TABLE TO THE RIGHT
 1505 PRINT " ";
 1510 FOR OF=SA TO (SA+15)
 1520 IF MA(OF)<32 THEN PRINT ".";
 1530 IF MA(OF)>126 THEN PRINT ".";
 1540 IF MA(OF)>31 AND MA(OF)<127 THEN PRINT CHR$(MA(OF));
 1550 NEXT OF
 1560 PRINT
 1570 RETURN
 
 1800 REM CALCULATE THE DIRECTORY SECTOR
 1810 RS = (MA(15) * 256) + MA(14)
 1820 PRINT "RS";RS
 1830 NF=MA(16)
 1840 PRINT "NF";NF
 1850 FS = (MA(37) * 256) + MA(36)
 1860 PRINT "FS";FS
 1870 DS = RS + (NF * FS)
 1880 PRINT "DIRECTORY SECTOR IS";DS
 1890 RETURN
 