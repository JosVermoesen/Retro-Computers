 10 REM TEST 1MB EXTENDED SRAM
 20 REM MEMORY BANK IS 8KB WINDOW TO 6502
 30 REM WINDOW IS AT $C000-$DFFF (49152-57343 DEC)
 40 REM BANK CONTROL REGISTER IS 7 BITS - 128 BANKS
 50 REM TEST ADDRESS AND DATA LINES ARE CONNECTED/NOT SHORTED
 60 S1=49152:REM START OF SRAM BANK (0XC000)
 70 E1=57343:REM END OF SRAM BANK (0XDFFF)
 80 B1=65492:REM BANK SELECT REGISTER (0XFFD4)
 90 C1=128:REM NUMBER OF 8K BANKS
 100 REM TEST ALL 8 BITS OF THE THE BANK REGISTER
 110 REM INCREMENTING BANK REGISTER BITS WRITE-READ
 120 PRINT "TEST RAM BANKING"
 130 PRINT "BANK REGISTER TEST - ";
 140 FOR BK=0 TO 255
 150 POKE B1,BK
 160 IF PEEK(B1)<>BK GOTO 200
 170 NEXT BK
 180 PRINT "PASSED"
 190 GOTO 300
 200 PRINT "FAILED":GOTO 1000
 300 REM WRITE BANK NUMBER TO FIRST LOCATION IN EACH BANK
 310 PRINT "TEST FIRST LOCATION OF EACH BANK - ";
 320 FOR BK=0 TO C1-1:REM STEP THROUGH THE BANKS
 330 POKE B1,BK:REM STORE BANK NUM TO BANK REG 
 340 POKE S1,BK:REM STORE BANK NUM TO FIRST LOC IN BANK
 350 NEXT BK
 400 REM READ BACK VALUES AND CHECK
 410 FOR BK=0 TO C1-1
 420 POKE B1,BK
 430 RD=PEEK(S1)
 440 IF RD=BK GOTO 480
 450 PRINT "FAILED"
 460 PRINT "  BANK =";BK
 470 PRINT "  VALUE =";RD:GOTO 1000
 480 NEXT BK
 490 PRINT "PASSED"
 600 PRINT "TESTING 8K MEMORY, FILLING 8KB BLOCK"
 610 TV=0:POKE B1,0
 620 FOR AD=S1 TO E1
 630 POKE AD,TV
 640 TV=TV+1
 650 TV=TV AND 255
 660 IF TV=0 THEN PRINT "W";
 670 NEXT AD
 700 PRINT:PRINT "FILL COMPLETE, READING 8KB BLOCK"
 710 TV=0
 720 FOR AD=S1 TO E1
 730 RD=PEEK(AD)
 740 IF RD=TV GOTO 800
 750 PRINT "8K VALUE BANK 1 TEST FAILED"
 760 PRINT "  ADDRESS =";AD
 770 PRINT "  EXPECTED =";TV
 780 PRINT "  READ VALUE =";RD
 790 GOTO 1000
 800 TV=TV+1
 810 TV=TV AND 255
 820 IF TV=0 THEN PRINT "R";
 830 NEXT AD
 840 PRINT:PRINT "8K VALUE BANK TEST PASSED"
 1000 REM
 