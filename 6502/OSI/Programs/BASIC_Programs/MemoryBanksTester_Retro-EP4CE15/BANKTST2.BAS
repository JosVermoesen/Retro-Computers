 100 REM TEST BANKED MEMORY SPACES
 110 REM MEMORY BANK HAS 4KB FROM $C000-$DFFF (49152-57343 DEC)
 120 PRINT "TEST RAM BANK"
 130 S1=49152:REM START OF SRAM BANK
 140 E1=57343:REM END OF SRAM BANK
 150 B1=65492:REM BANK SELECT REGISTER $FFD4
 160 C1=64:REM NUMBER OF 8K BANKS
 170 PRINT "BANK REGISTER TEST - ";
 180 FOR BK=0 TO C1-1
 190 POKE B1,BK
 200 IF PEEK(B1)<>BK GOTO 240
 210 NEXT BK
 220 PRINT "PASSED"
 230 GOTO 300
 240 PRINT "FAILED":GOTO 1000
 300 REM WRITE BANK NUMBER TO FIRST LOCATION IN EACH BANK
 310 PRINT "TEST FIRST LOCATION OF EACH BANK - ";
 320 FOR BK=0 TO C1-1
 330 POKE B1,BK:POKE S1,BK
 340 NEXT BK
 400 REM READ BACK VALUES AND CHECK
 410 FOR BK=0 TO C1-1
 420 POKE B1,BK
 430 RD=PEEK(S1)
 440 IF RD=BK GOTO 480
 450 PRINT "FAILED "
 460 PRINT "  BANK =";BK
 470 PRINT "  VALUE =";RD:GOTO 1000
 480 NEXT BK
 490 PRINT "PASSED"
 600 PRINT "TESTING 8K MEMORY, FILLING 8KB BLOCK"
 610 TV=0:POKE B1,0
 620 FOR AD=S1 TO E1
 630 POKE AD,TV
 640 TV=TV+1
 650 TV=TV AND 255
 660 IF TV=0 THEN PRINT "W";
 670 NEXT AD
 700 PRINT:PRINT "FILL COMPLETE, READING"
 710 TV=0
 720 FOR AD=S1 TO E1
 730 IF PEEK(AD)=TV GOTO 770
 740 PRINT "8K VALUE BANK 1 TEST FAILED - ADDRESS ";AD;
 750 PRINT "VALUE ";PEEK(AD)
 760 GOTO 1000
 770 TV=TV+1
 780 TV=TV AND 255
 790 IF TV=0 THEN PRINT "R";
 800 NEXT AD
 810 PRINT:PRINT "8K VALUE BANK 1 TEST PASSED"
 1000 REM
 