
REM Death Ship Adventure   STRING FIX?

 10 POKE 133,250:POKE134,30:POKE11,250:POKE12,30:L=7930:K=L
 20 FORI=45383TO45644:X=PEEK(I):POKEL,X:L=L+1:NEXT
 30 FORT=1TO13:READX,Y:POKEX,Y:NEXT
 40 DATA7964,31,7963,140,7989,31,7988,134,8014,31,8013,203,8067,31,8066
 50 DATA 140,8191,30,8190,254,7997,4,8146,2,8147,24
 60 NEW

RUN

LOAD
REM Death Ship Adventure Main Program

 100 FORT=1TO30:PRINT:NEXT:T1=5:T2=0:CA=23
 110 DIMP(18,6),O$(44),L(44),D$(18):GOSUB550:GOTO1010
 120 PRINT"TELL ME WHAT TO DO. ";:GOSUB 1730
 130 IFL(32)=L(18)ANDL(18)<>0THEN1620
 140 IFA$="HE"THEN1660
 150 IFF3=1THENFT=FT+1:IFFT=25ORFT=30THENPRINT"LIGHT GETTING DIM."
 160 IFFT>33THENF3=0
 170 T2=T2+1:IFT2=60THENT2=0:T1=T1+1:IFT1=9THEN1520
 180 IFCA=24THENPRINT"IT'S TICKING LOUDER.":T3=T3+1:IFT3=14THEN1530
 190 IFF4>0THENPRINT"IT'S LEAKING.":F4=F4+1:IFF4=7THENF4=0:L(32)=18
 195 IFA$="CL"ORA$="JU"THENA$="GO"
 200 IFA$="GE"THENA$="TA"
 205 IFB$="RA"ANDL=6THENB$="ST"
 210 IFA$="SM"ORA$="BR"THENA$="HI"
 220 IFA$="TH"ORA$="PO"THENA$="DR"
 230 IFA$="IN"THENY=0:GOTO520
 240 FORY=1TOLEN(W$):IFMID$(W$,Y,2)=A$THENF=(Y+1)/2:GOTO260
 250 X=USR(X):NEXT
 260 FORX=1TOLEN(O$)STEP2:IFMID$(O$,X,2)=B$THENS=(X+1)/2:GOTO280
 270 UU=USR(UU):NEXT
 280 IFF1=1ANDF<>1THENPRINT"I DROWNED, I TOLD YOU I CAN'T SWIM....":END
 290 IFF<1THEN120
 300 ON(F-1)GOTO360,480,1440,1390,1350,1240,1010,500,490,1560
 310 IFS=5ANDL(18)=0THENPRINT"CAN'T CARRY THAT MUCH.":GOTO120
 320 IFX>88ORY>22THENPRINT" HUH?":GOTO120
 330 F1=0:IFS=31ANDL(9)<>5THENPRINT"CAN'T YET, TOO HIGH.":GOTO120
 340 IFS=31ANDL(9)=5THENL=3:L2=3:GOTO1160
 350 GOTO890
 360 IFS=19ANDL(14)=0THENF7=1
 370 IFB$="WA"THEN1580
 375 IFS=8THENO$(8)="ROPE":F2=0
 380 IFB$="HI"ANDL=10ANDL(17)=0THEN1510
 390 IFS>CATHENPRINT"I CAN'T.":GOTO120
 400 IFS=19ANDL(14)<>0THENPRINT"NO HAMMER.":GOTO120
 410 IFS=11ANDF7=0THENPRINT"IT'S NAILED DOWN.":L(13)=1:GOTO120
 420 IFS=11ANDL(8)=50THENL(8)=1
 430 IFS<7THEN120
 440 IFS=8THENF2=0
 450 IFY>88ORL(S-6)<>LTHENPRINT"I DON'T SEE IT.":GOTO120
 460 IFC=7THENPRINT"I CAN'T.  I'M CARRING TOO MUCH.":GOTO 120
 470 IFC<7ANDL(S-6)=LTHENL(S-6)=0:C=C+1
 480 IFF=3ANDS>6ANDL(S-6)=0THENL(S-6)=L:C=C-1
 490 IFF=10ANDS=25THENF3=0
 500 IFF=9ANDS=22ANDL(16)=0THENF3=1:PRINT"OPPOSITE OF LIGHT=UN LIGHT
 510 GOTO 120
 520 PRINT"I'M CARRYING":FORX=1TO38:IFL(X)=0THENPRINTO$(X+6):Y=Y+1
 530 NEXT:IFY=0THENPRINT"NOTHING."
 540 GOTO 120
 550 FORX=1TO44:READO$(X):NEXT
 560 DATA NORTH,SOUTH,EAST,WEST,UP,DOWN
 570 DATA WIRE,ROPE,KEY,AXE,RADIO,GLOVES,MESSAGE,CUTTER
 580 DATACRATE,PRESERVER,BUCKET,MOP,NAILS,HAMMER
 590 DATA TIMEPIECE,FLASHIGHT,SCREW DRIVERS
 600 DATA BOMB
 610 DATA LIFEBOAT,PORTHOLE,TABLE,SWIMMING POOL
 620 DATA DOOR TO HOLD-LOCKED OTHER SIDE,PORTHOLE,MAST,PORTHOLE
 630 DATA HEAVY HINGED LOCKED DOOR
 640 DATA ANCHOR CHAIN HANGING OVER THE SIDE
 650 DATA EMERGENCY LOCKER,LOCKED DOOR- HAS WINDOW
 660 DATA SUPPLY CABINET,WATER,STARBOARD RAILING,LOCKED BRIG
 670 DATA HINGES,A FAINT TICKING NOISE,SIGN-ENGINE ROOM AFT
 680 DATA CLOSED DOOR
 690 W$="GOTADRHITIREOPLOLIUNCU"
 700 O$="NOSOEAWEUPDOWIROKEAXRAGLMECUCRPRBUMONAHATIFLSCBOLIPOT"
 710 O$=O$+"ASWDOPOMAPOHEANEMLOSUWASTLOHIA SICL"
 715 UU=USR(UU)
 720 FORX=1TO38:READL(X):NEXT:L=1
 730 DATA50,3,14,17,1,30,1,50,10,5,11,11,50,10,2,29,31
 740 DATA9,7,14,14,7,12,4,5,16,10,5,8,12,13,18,6,15,31,10,10,10
 750 FORY=1TO18:FORX=1TO6:READP(Y,X):NEXTX,Y
 760 DATA,,,,,7,,,,,,6,,,,,,5
 770 DATA,,,,6,,,6,,,,,5,7,,,2,12,6,,,,1,
 780 DATA7,,,,,,10,,,,,-1,,-1,,,,15,,,,12,,
 790 DATA,,11,13,6,,,,12,,,,,,,,,,,,16,,10
 800 DATA,,,,,5,,,15,,,,,,7,,,,
 810 FORX=1TO18:READD$(X):NEXT
 820 DATA RADIO ROOM,SHIPS BRIDGE
 830 DATA TOP OF FLAG POLE.,OVER THE SIDE,ON THE BOW
 840 DATA MIDSHIPS DECK,AFT DECK,A LIFE BOAT,ENGINE ROOM
 850 DATA THE HOLD,JANITORS CLOSET,A HALL WAY,A DOCTORS CABIN
 860 DATA CAPTAINS CABIN,A WET SMELLY BILGE
 870 DATA ON AN ANCHOR CHAIN,THE BRIG,SWIMMING POOL
 880 RETURN
 890 IF(B$="SW"ORB$="PO")ANDL=7THEN1540
 900 IFB$<>"PO"ANDB$<>"LI"THEN960
 910 IFL<>8ANDL<>7ANDL<>15ANDL<>16ANDL<>4ANDL<>14THEN960
 920 IFL=4THENL=14:L2=L:GOTO1160
 930 IFL=14THENL=4:L2=4:GOTO1160
 940 IFL/2=INT(L/2)THENL=L-2
 946 L=L+1:L2=L:GOTO1160
 960 IFB$="AN"AND(L=5ORL=10)THENL=16:L2=16:GOTO1160
 970 IF(B$="ST"ORB$="RA")AND(F2=0ORL(6)<>0)THEN1370
 980 IF(B$="ST"ORB$="RA")ANDL=6THENL=4:L2=4:GOTO1160
 990 L2=L:IFS>6THENPRINT"CAN'T GO THERE.":GOTO120
 1000 IFP(L,S)>0THENL=P(L,S):L2=L:GOTO1160
 1010 IFB$="AR"ORB$=""THENL2=L:GOTO1160
 1020 IFB$="TI"ANDL(15)=0THENPRINT"TIME IS"T1":"T2:GOTO120
 1030 IFLO=7ANDB$="LI"THENL2=8
 1040 IFB$<>"PO"THEN1090
 1050 IFL=7THENL2=18
 1060 IFL=16ANDF3=1THENL2=15
 1070 IFL=4THENL2=14
 1080 IFL=15THENL2=16
 1090 IFL=12ANDB$="WI"THENL2=14
 1100 IFL=15ANDB$="BR"THENL2=17
 1110 IFL=5ANDB$="MA"THENL3=3
 1120 IFL=6ANDB$="ST"THENL2=4
 1130 IFL=5ANDB$="AN"THENL2=16
 1140 IFL2<>LTHEN1180
 1150 PRINT"I SEE NOTHING SPECIAL.":GOTO120
 1160 PRINT"I AM IN "D$(L)".":Y=0
 1170 IF(L=10ORL=15ORL=9)AND(F3=0ORL(16)<>0)THEN1380
 1180 PRINT"I SEE:":FORX=1TO38:IFL(X)=L2THENPRINT" *"O$(X+6);:Y=Y+1
 1185 IFL(X)=L2ANDCA<24ANDO$(X+6)="BOMB"THENPRINT" WIRED TO A POST";
 1190 NEXT:PRINT:L2=L:IFY=0THENPRINT"NOTHING.":PRINT
 1200 PRINT"OBVIOUS EXITS ARE:":FORX=1TO6:IFP(L,X)>0THENPRINTO$(X)
 1210 NEXT:GOTO120
 1220 L2=80:IFB$="WI"THENL2=14:IFL=14THENL2=12
 1230 GOTO1180
 1240 FORX=1TO44
 1250 IFL=8ANDB$="LO"ANDL(X)=29THENL(X)=8:O$(35)="OPEN LOCKER"
 1260 IFB$="CA"THENS=37
 1270 IFL=13ANDS=37ANDL(X)=30THENL(X)=13:O$(37)="OPEN CAB."
 1280 NEXT
 1290 IFL=15ANDB$="BR"ANDL(3)=0THENP(15,1)=17:O$(40)="OPEN BRIG"
 1300 IFL=14ANDB$="DO"THENP(14,2)=12:O$(36)="OPEN DOOR":P(12,1)=14
 1310 IFL=10ANDB$="DO"THEN1330
 1320 GOTO120
 1330 P(10,1)=12:P(12,2)=10:O$(29)="OPEN DOOR":O$(44)=O$(29)
 1340 GOTO120
 1350 IFL(S-6)<>0THENPRINT"I DON'T HAVE IT.":GOTO120
 1360 PRINT"ABANDONING SHIP-NO CHANCE OF FINDING BOMB BY 9:00"
 1365 GOTO120
 1370 PRINT"MY HANDS SLIPPED! I AM  DROWNING IN THE SEA.":END
 1380 PRINT"ITS DARK. I CAN'T SEE.":GOTO 120
 1390 IFS<>8ORL(2)<>0ORL<>6THENPRINT"CAN'T YET.":GOTO120
 1400 INPUT"TO WHAT";A$:O$(8)="ROPE TIED TO "+A$
 1410 IFLEN(A$)>2THENA$=LEFT$(A$,2)
 1420 IF(A$="ST"ORA$="RA")ANDL=6ANDL(2)=0THENF2=1
 1425 L(2)=L
 1430 GOTO120
 1440 PRINT"WITH WHAT":GOSUB1690
 1450 IFA$="FO"ORA$="FI"THENPRINT"YOU'RE KIDDING.":GOTO120
 1460 IFL(4)<>0ORA$<>"AX"THEN1490
 1470 IFB$="DO"THENPRINT"STEEL DOOR-";
 1480 IFS=15ANDL(9)=LTHEN1500
 1490 PRINT"NOTHING HAPPENED.":GOTO120
 1500 L(17)=L(9):O$(15)="SPLINTERS":GOTO 120
 1510 L(41)=0:O$(33)="DOOR LAYING ON FLOOR":P(10,2)=9:GOTO120
 1520 PRINT:PRINT:PRINT"THE TIME IS UP.........":PRINT
 1525 FORT=1TO50:IFT<21THENPRINT"TIC"
 1526 PRINT:FORTT=1TO120:NEXTTT,T:POKE56900,0
 1530 PRINT"BBOOOOOOOM !!!!!!!!":FORT=1TO5000:NEXT:POKE56900,1:END
 1540 IFL(10)<>0THENPRINT"HURRY, I CAN'T SWIM.":F1=1
 1550 L=18:L2=18:GOTO1160
 1560 IFL(8)=0ANDB$="WI"ANDL=9THENCA=24:L(1)=9:GOTO120
 1570 PRINT"NOTHING HAPPENED.":GOTO120
 1580 PRINT"IN WHAT";:GOSUB1690
 1590 IFL<>18ORL(11)<>0ORA$<>"BU"THEN1610
 1600 L(32)=0:F4=1:GOTO120
 1610 PRINT"I DON'T THINK IT'L WORK":GOTO120
 1620 PRINT"WHEW!!!":PRINT"THE BOMB IS DISARMED."
 1630 PRINT"THE TIME IS "T1":"T2
 1640 PRINT:PRINT"   CONGRATULATIONS":PRINT
 1650 PRINT"MAYBE NEXT TIME YOU WON'T CUT IT SO CLOSE.":END
 1660 IFL=5THENPRINT"NOT ALL EXITS OBVIOUS"
 1670 IFL=1ANDL(8)=50THENPRINT"THERE IS SOMETHING HERE YOU NEED"
 1675 IFL=6THENPRINT"LOOK AROUND CAREFULY"
 1677 IFL<>5ANDL<>6ANDL<>1THENPRINT"I DON'T KNOW, ITS YOUR  GAME."
 1680 GOTO120
 1690 INPUTA$:IFLEN(A$)>1THENA$=LEFT$(A$,2)
 1700 UU=USR(UU):RETURN
 1710 FORX=1TOLEN(A$)
 1715 IFMID$(A$,X,1)=" "ANDLEN(A$)>X+1THENB$=MID$(A$,X+1,2)
 1720 UU=USR(UU):NEXT
 1730 INPUTA$:FORX=1TOLEN(A$)
 1740 IFMID$(A$,X,1)=" "ANDLEN(A$)>X+1THENB$=MID$(A$,X+1,2)
 1750 NEXT:IFLEN(A$)>1THENA$=LEFT$(A$,2)
 1755 UU=USR(UU)
 1760 RETURN

